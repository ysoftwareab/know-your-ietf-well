Zero          = %x30          ; the ASCII character '0'

ProfileChars  = DIGIT / ALPHA / %x2D-2F / %x3A / %x5F
                                   ; digits, alphas, and the characters
                                   ; '-', '.', '/', ':', '_'

QuotedString  = DQUOTE *(VCHAR / WSP) DQUOTE

HexString     = 1*(HEXDIG HEXDIG)

Id            = 1*DIGIT       ; identifier for an SMX transaction

Script        = QuotedString  ; script file name

RunId         = 1*DIGIT       ; globally unique identifier for a
                                   ; running script (note, smRunIndex
                                   ; is only unique for a smLaunchOwner,
                                   ; smLaunchName pair)

Profile       = 1*ProfileChars ; security profile name

RunState      =  "1"          ; smRunState `initializing'
RunState      =/ "2"          ; smRunState `executing'
RunState      =/ "3"          ; smRunState `suspending'
RunState      =/ "4"          ; smRunState `suspended'
RunState      =/ "5"          ; smRunState `resuming'
RunState      =/ "6"          ; smRunState `aborting'
RunState      =/ "7"          ; smRunState `terminated'
ExitCode      =  "1"          ; smRunExitCode `noError'
ExitCode      =/ "2"          ; smRunExitCode `halted'
ExitCode      =/ "3"          ; smRunExitCode `lifeTimeExceeded'
ExitCode      =/ "4"          ; smRunExitCode `noResourcesLeft'
ExitCode      =/ "5"          ; smRunExitCode `languageError'
ExitCode      =/ "6"          ; smRunExitCode `runtimeError'
ExitCode      =/ "7"          ; smRunExitCode `invalidArgument'
ExitCode      =/ "8"          ; smRunExitCode `securityViolation'
ExitCode      =/ "9"          ; smRunExitCode `genericError'

Cookie        = HexString     ; authentication cookie

Version       = "SMX/1.1"     ; current version of the SMX protocol

Argument      = HexString / QuotedString      ; see smRunArgument

Result        = HexString / QuotedString      ; see smRunResult

ErrorMsg      = HexString / QuotedString      ; see smRunError


Command =  "hello"   WSP Id CRLF

Command =/ "start"   WSP Id WSP RunId WSP Script WSP Profile
                          WSP Argument CRLF

Command =/ "suspend" WSP Id WSP RunId CRLF

Command =/ "resume"  WSP Id WSP RunId CRLF

Command =/ "abort"   WSP Id WSP RunId CRLF

Command =/ "status"  WSP Id WSP RunId CRLF

Reply =  "211" WSP Id WSP Version WSP Cookie CRLF
                                   ; identification of the
                                   ; runtime system

Reply =/ "231" WSP Id WSP RunState CRLF
                                   ; status of a running script

Reply =/ "232" WSP Id CRLF    ; abort of a running script

Reply =/ "401" WSP Id CRLF    ; syntax error in command

Reply =/ "402" WSP Id CRLF    ; unknown command

Reply =/ "421" WSP Id CRLF    ; unknown or illegal Script

Reply =/ "431" WSP Id CRLF    ; unknown or illegal RunId

Reply =/ "432" WSP Id CRLF    ; unknown or illegal Profile

Reply =/ "433" WSP Id CRLF    ; illegal Argument

Reply =/ "434" WSP Id CRLF    ; unable to change the status of
                                   ; a running script

Reply =/ "511" WSP Zero WSP QuotedString CRLF
                                   ; an arbitrary message send from
                                   ; the runtime system

Reply =/ "531" WSP Zero WSP RunId WSP RunState CRLF
                                   ; asynchronous running script
                                   ; status change

Reply =/ "532" WSP Zero WSP RunId WSP RunState WSP Result CRLF
                                   ; intermediate script result

Reply =/ "533" WSP Zero WSP RunId WSP RunState WSP Result CRLF
                                   ; intermediate script result that
                                   ; triggers an event report
Reply =/ "534" WSP Zero WSP RunId WSP Result CRLF
                                   ; normal script termination,
                                   ; deprecated

Reply =/ "535" WSP Zero WSP RunId WSP ExitCode WSP ErrorMsg CRLF
                                   ; abnormal script termination,
                                   ; deprecated

Reply =/ "536" WSP Zero WSP RunId WSP RunState WSP ErrorMsg CRLF
                                   ; script error

Reply =/ "537" WSP Zero WSP RunId WSP RunState WSP ErrorMsg CRLF
                                   ; script error that
                                   ; triggers an event report

Reply =/ "538" WSP Zero WSP RunId WSP ExitCode CRLF
                                   ; script termination
