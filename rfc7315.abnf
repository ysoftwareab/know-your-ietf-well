ccf=192.0.8.1; ecf=192.0.8.3,
ccf-2=192.0.8.2; ecf-2=192.0.8.4

icid-generated-at=192.0.6.8;
orig-ioi=home1.net

P-Associated-URI       = "P-Associated-URI" HCOLON
                                  [p-aso-uri-spec]
                                  *(COMMA p-aso-uri-spec)
p-aso-uri-spec         = name-addr *(SEMI ai-param)
ai-param               = generic-param

P-Called-Party-ID      = "P-Called-Party-ID" HCOLON
                                  called-pty-id-spec
called-pty-id-spec     = name-addr *(SEMI cpid-param)
cpid-param             = generic-param

P-Visited-Network-ID   = "P-Visited-Network-ID" HCOLON
                                   vnetwork-spec
                                   *(COMMA vnetwork-spec)
vnetwork-spec          = (token / quoted-string)
                                   *(SEMI vnetwork-param)
vnetwork-param         = generic-param

P-Access-Network-Info  = "P-Access-Network-Info" HCOLON
                                access-net-spec *(COMMA access-net-spec)
access-net-spec        = (access-type / access-class)
                               *(SEMI access-info)
access-type            = "IEEE-802.11" / "IEEE-802.11a" /
                               "IEEE-802.11b" / "IEEE-802.11g" /
                               "IEEE-802.11n" /
                               "IEEE-802.3" / "IEEE-802.3a" /
                               "IEEE-802.3ab" / "IEEE-802.3ae" /
                               "IEEE-802.3ak" / "IEEE-802.3ah" /
                               "IEEE-802.3aq" / "IEEE-802.3an" /
                               "IEEE-802.3e" / "IEEE-802.3i" /
                               "IEEE-802.3j" / "IEEE-802.3u" /
                               "IEEE-802.3y" / "IEEE-802.3z" /
                               "3GPP-GERAN" /
                               "3GPP-UTRAN-FDD" / "3GPP-UTRAN-TDD" /
                               "3GPP-E-UTRAN-FDD" / "3GPP-E-UTRAN-TDD" /
                               "3GPP2-1X-Femto" / "3GPP2-UMB" /
                               "3GPP2-1X-HRPD" / "3GPP2-1X" /
                               "ADSL" / "ADSL2" / "ADSL2+" / "RADSL" /
                               "SDSL" / "HDSL" / "HDSL2" / "G.SHDSL" /
                               "VDSL" / "IDSL" /
                               "DOCSIS" / "GSTN" / "GPON" / " XGPON1" /
                               "DVB-RCS2" / token
access-class           = "3GPP-GERAN" /  "3GPP-UTRAN" /
                               "3GPP-E-UTRAN" / "3GPP-WLAN" /
                               "3GPP-GAN" / "3GPP-HSPA" /
                               "3GPP2" / token
access-info            = cgi-3gpp / utran-cell-id-3gpp /
                               dsl-location / i-wlan-node-id /
                               ci-3gpp2 / eth-location /
                               ci-3gpp2-femto / fiber-location /
                               np / gstn-location /local-time-zone /
                               dvb-rcs2-node-id / extension-access-info
np                     = "network-provided"
extension-access-info  = gen-value
cgi-3gpp               = "cgi-3gpp" EQUAL
                                   (token / quoted-string)
utran-cell-id-3gpp     = "utran-cell-id-3gpp" EQUAL
                                   (token / quoted-string)
i-wlan-node-id         = "i-wlan-node-id" EQUAL
                                   (token / quoted-string)
dsl-location           = "dsl-location" EQUAL
                                   (token / quoted-string)
eth-location           = "eth-location" EQUAL
                                   (token / quoted-string)
fiber-location         = "fiber-location" EQUAL
                                   (token / quoted-string)
ci-3gpp2               = "ci-3gpp2" EQUAL
                                   (token / quoted-string)
ci-3gpp2-femto         = "ci-3gpp2-femto" EQUAL
                                    (token / quoted-string)
gstn-location          = "gstn-location" EQUAL
                                    (token / quoted-string)
dvb-rcs2-node-id       = "dvb-rcs2-node-id" EQUAL
                                     quoted-string
local-time-zone        = "local-time-zone"  EQUAL
                                    quoted-string
operator-specific-GI   = "operator-specific-GI" EQUAL
                                    (token / quoted-string)
utran-sai-3gpp         = "utran-sai-3gpp" EQUAL
                                    (token / quoted-string)

P-Charging-Addresses = "P-Charging-Function-Addresses" HCOLON
                          charge-addr-params *(COMMA charge-addr-params)
charge-addr-params   = charge-addr-param *(SEMI charge-addr-param)
charge-addr-param    = ccf / ecf / ccf-2 /ecf-2 / generic-param
ccf                  = "ccf" EQUAL gen-value
ecf                  = "ecf" EQUAL gen-value
ccf-2                = "ccf-2" EQUAL gen-value
ecf-2                = "ecf-2" EQUAL gen-value

   The P-Charging-Function-Addresses header field contains one or two
   addresses of the ECF (ecf and ecf-2) or CCF (ccf and ccf-2).  The
   first address of the sequence is ccf or ecf.  If the first address of
   the sequence is not available, then the next address (ccf-2 or ecf-2)
   MUST be used if available.
P-Charging-Vector  = "P-Charging-Vector" HCOLON icid-value
                                  *(SEMI charge-params)
charge-params      = icid-gen-addr / orig-ioi / term-ioi /
                           transit-ioi / related-icid /
                           related-icid-gen-addr / generic-param
icid-value                = "icid-value" EQUAL gen-value
icid-gen-addr             = "icid-generated-at" EQUAL host
orig-ioi                  = "orig-ioi" EQUAL gen-value
term-ioi                  = "term-ioi" EQUAL gen-value
transit-ioi               = "transit-ioi" EQUAL transit-ioi-list
transit-ioi-list          = DQUOTE transit-ioi-param
                                     *(COMMA transit-ioi-param) DQUOTE
transit-ioi-param         = transit-ioi-indexed-value /
                                  transit-ioi-void-value
transit-ioi-indexed-value = transit-ioi-name "."
                                                transit-ioi-index
transit-ioi-name          = ALPHA *(ALPHA / DIGIT)
transit-ioi-index         = 1*DIGIT
transit-ioi-void-value    = "void"
related-icid              = "related-icid" EQUAL gen-value
related-icid-gen-addr     = "related-icid-generated-at" EQUAL host

