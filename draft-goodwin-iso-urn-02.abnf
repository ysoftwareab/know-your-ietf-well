NSS           = std-nss

      All URNs conforming to this specification begin the NSS with the
      prefix "std:", to denote the restriction to documents developed by
      the ISO standards development procedures as defined in the ISO/IEC
      Directives, Part 1 [ISODIR-1] and the ISO Supplement [ISODIR-S].
      Prefixes that identify ISO objects of other kinds may be defined
      in future revisions of this specification.

std-nss       = "std:" docidentifier *supplement *docelement
                   [addition]

      The prefix "std:" distinguishes an <std-nss>.  An <std-nss>
      identifies the ISO document that is designated by the
      <docidentifier>, as extended or modified by any identified
      <supplement>.  (An <std-nss> that identifies all parts of a
      multipart ISO document is a special case as described under the
      element <partnumber>.)  If the <std-nss> contains an <addition>
      element, the NSS identifies a resource extracted from the ISO
      document or otherwise associated with it (see below).

docidentifier = originator [":" type] ":" docnumber [":" partnumber]
                   [[":" status] ":" edition]
                   [":" docversion] [":" language]

      <docidentifier> provides the complete identification of an ISO
      document.  Each of its component elements is described below.

originator    = "iso" / "iso-iec" / "iso-cie" / "iso-astm" /
                   "iso-ieee" / "iec"

      <originator> is the organization (usually an international body)
      from which a document emanates.

      Current values:

iso      = International Organization for Standardization

iec      = International Electrotechnical Commission (IEC), or
                 Commission Electrotechnique Internationale

iso-iec  = jointly developed by ISO and IEC

iso-cie  = jointly developed by ISO and the Commission
                 Internationale d'Eclairage (CIE)

iso-astm = jointly developed by ISO and the American Society for
                 Testing and Materials International (ASTM)

iso-ieee = jointly developed by ISO and the Institute for
                 Electrical and Electronics Engineers (IEEE)

type          = "data" / "guide" / "isp" / "iwa" /
                   "pas" / "r" / "tr" / "ts" / "tta"

      <type> designates the ISO deliverable type.  If the <type> element
      is not present, the classification is "international standard".
      Other current values:

data  = Data (document type no longer published)
guide = Guide

isp   = International Standardized Profile

iwa   = International Workshop Agreement

pas   = Publicly Available Specification

r     = Recommendation (document type no longer published)

tr    = Technical Report

ts    = Technical Specification

tta   = Technology Trends Assessment

docnumber     = DIGITS

      <docnumber> is the reference number assigned to the document by
      ISO and/or IEC.  An ISO document may comprise a single document,
      or two or more separate parts each of which is identified by
      <partnumber>.

partnumber    = "-" 1*( DIGIT / ALPHA / "-" )

      <partnumber> is the reference number that identifies a part of a
      multipart standard.

      Where it is required to refer to a multipart ISO document in its
      entirety, this can be designated by omitting the <partnumber>
      element.  However, this precludes the possibility of using any
      further elements except <addition>.

      _NOTE_ The option to refer to a multipart ISO document by omitting
      the <partnumber> element has been included to align with the
      provision in the ISO/IEC Directives, Part 2, 2004 [ISODIR-2]
      subclause 6.2.2 of making an undated reference to all parts of an
      ISO document.  It is only permissible to use this option where the
      URN is referring to a multipart ISO document in its entirety.
      Since the use of this option precludes the designation of the
      elements <status> and <edition>, it is implicit that the URN needs
      to remain valid irrespective of any future changes to the
      multipart document (see the rules for undated references given in
      the ISO/IEC Directives, Part 2, 2004 [ISODIR-2] subclause
      6.6.7.5.2).  This shall be taken into consideration in the use
      (and maintenance) of any URN specification employing this option.
      _NOTE_ In the case where the multipart document comprises
      different types of ISO deliverable, the <type> of the core part
      (usually part 1) applies.  See the example "Reference to a
      resource related to all parts of a multipart document".

      Except for the case where it is required to refer to a multipart
      document in its entirety, the element <partnumber> is required if
      the identified resource is a part of an ISO document.  Otherwise,
      this element is not used.

status        = ( "draft" / "cancelled" ) / stage

      <status> indicates the publication status of the document.  When
      the <status> element is not present, the NSS refers to a published
      document.  Other values:

draft     = document that has not yet been accepted for
                  publication by international ballot

cancelled = document that has been deleted or withdrawn

stage         = "stage-" stagecode ["." iteration]

      <stage> indicates the stage code and iteration of the document.

stagecode     = DIGIT DIGIT "."  DIGIT DIGIT

      <stagecode> is the harmonized stage code in accordance with ISO
      Guide 69:1999, "Harmonized Stage Code system (Edition 2) --
      Principles and guidelines for use" [ISOGUIDE69].

iteration     = "v" DIGITS

      <iteration> is a sequential number which refers to a specific
      iteration of the project's lifecycle through the designated stage.

      If no <iteration> is specified the reference is to the highest
      iteration available for the specified stagecode.

      _NOTE_ In the ISO Central Secretariat project management database
      the <iteration> is referred to as the "project version".

edition       = "ed-" DIGITS

      <edition> designates a specific edition of the document.  (DIGITS
      is the (sequential) edition number.)  If no <edition> is
      specified, the NSS refers to the latest edition.
docversion    = "v" (simpleversion / isoversion)

simpleversion = DIGITS

      <docversion> designates the version number of a document's
      <edition>.  It is altered by correction (corrected version;
      Technical Corrigendum) or amendment (Amendment; Addendum) and is
      distinct from a revision, which changes the edition number.

      In the <simpleversion>, the first version published is 1, and each
      subsequent correction or amendment increases the version number by
      1.

      If no <docversion> is specified, the reference is to the highest
      version number available for the denoted <edition>.

      Current values of <simpleversion>:

         1 - first version published

         2 - corrected version published

isoversion    = baseversion *includedsuppl

baseversion   = DIGITS

includedsuppl = "-" suppltype supplnumber [ "." supplversion ]

      An <isoversion> can be linked to a simpleversion by defining an
      existing simpleversion as baseversion and listing all the
      <supplement> elements (corrections and amendments) incorporated
      into that version.

      Examples for the <isoversion> (internal ISO version) scheme:

         1 = first version of standard

         1-amd1.v1 = first version of standard incorporating first
         version of Amendment 1

         1-amd1.v1-amd2.v1 = first version of standard incorporating
         first version of Amendment 1 and first version of Amendment 2

         1-amd1.v2-amd2.v1-amd3 = first version of standard
         incorporating corrected version of Amendment 1, first version
         of Amendment 2, and highest version of Amendment 3
         1-cor3 = first version of standard incorporating highest
         version of Technical Corrigendum 3

         1-amd1-cor3 = first version of standard incorporating highest
         version of Amendment 1 and highest version of Technical
         Corrigendum 3

language      = monolingual / bilingual / trilingual

monolingual   = "en" / "fr" / "ru" / "es" / "ar"

bilingual     = "en,fr" / "en,ru" / "fr,ru"

trilingual    = "en,fr,ru"

      <language> designates the official ISO language(s), or the
      language of an official translation, in which the document
      (object) is processed and published by ISO (excluding languages
      which constitute only specific elements of the content).  The
      value is one or more alpha-2 codes, each of which designates a
      language, as specified in ISO 639-1 [ISO639-1].  If no language
      element is specified, <en> is assumed.

      _NOTE_ Although [ISO639-1] recommends that language codes be
      written in lowercase this ABNF definition allows the use of
      uppercase language codes because in ABNF [RFC4234], terminal
      symbols defined as literal strings are explicitly case-
      insensitive.  This case distinction does not carry any meaning
      (see Section 2.9) and it is recommended to use language codes in
      lowercase.  For additional information about the usage of language
      tags in information objects see [RFC4646].

supplement    = ":" suppltype ":" supplnumber
                   [":" supplversion ] [":" language ]

suppltype     = "amd" / "cor" / "add"

supplnumber   = DIGITS

supplversion  = "v" DIGITS

      <supplement> designates a technical alteration of or addition to
      an ISO standard that does not result in a new <edition> or
      <version>.  Each <supplement> may be one of the three types,
      designated by <suppltype>:
amd = Amendment -- a document that alters and/or adds to
            previously agreed technical provisions in an existing ISO
            document; an amendment is subject to acceptance by ballot in
            accordance with the ISO/IEC Directives, Part 1, 2004
            [ISODIR-1] subclause 2.10.3

cor = Technical Corrigendum -- a document that corrects a
            technical error or ambiguity, or updates the ISO document in
            such a way that the modification has no effect on the
            technical normative elements; a Technical Corrigendum is not
            balloted -- see the ISO/IEC Directives, Part 1, 2004
            [ISODIR-1] subclause 2.10.2

add = Addendum -- (document type no longer published) Addenda were
            documents that changed (by correction, addition or deletion)
            the technical requirements of an ISO document; an addendum
            was subject to acceptance by ballot in accordance with the
            ISO/IEC Directives, Part 1.  (Addenda are included in this
            RFC because some of them are still valid.)

docelement    = ":" ( "clause" / "figure" / "table" / "term" ) ":"
                   elementnumber / elementrange
                   *( "," elementnumber / elementrange )

elementnumber = ( ALPHA / DIGITS ) *( "."  DIGITS )
elementrange  = elementnumber "-" elementnumber

      <docelement> identifies one or more numbered subdivisions of a
      document.  Types of numbered subdivision are specified in the ISO/
      IEC Directives, Part 2 [ISODIR-2].  This RFC currently specifies
      forms for reference to clauses, figures, tables and terms only.
      It does not provide for reference to subfigures.  Revisions of
      this specification may define additional values.

      <clause> represents the selection of one or more clauses or
      subclauses of the document. <figure> represents the selection of
      one or more figures of the document. <table> represents the
      selection of one or more tables of the document. <term> represents
      the selection of one or more terms of the document

      <elementnumber> designates a numbered subdivision in a document,
      where the type of subdivision is identified by the literal
      "clause", "figure", "table" or "term".  When the first character
      of <elementnumber> is a digit, the reference is to the subdivision
      designated by that digit string and by any additional digit
      strings separated by periods.  When the first character of
      <elementnumber> is alphabetical, the reference is to the
      corresponding Annex, and to the subdivisions designated by
      additional digit strings.

      The form <elementnumber> HYPHEN <elementnumber> designates a range
      of subdivisions and the form <elementnumber> COMMA <elementnumber>
      a list.  A list can contain ranges.

addition      = techdefined / isodefined

techdefined   = ":tech" *techelement

techelement   = ; unspecified

isodefined    = ; unspecified

      <addition> is an additional element of the NSS intended to
      identify a representation of an ISO document, an extract from an
      ISO document, or some related information set, as a resource in
      its own right.

      <techdefined> represents an associated or embedded resource
      defined by the committee that develops or maintains the identified
      document.  All such <addition> begin with the prefix ":tech".

      <isodefined> represents associated or embedded resources defined
      by the ISO Central Secretariat.  The definition of an <addition>
      element beginning with any symbol other than <tech> is reserved to
      the ISO Central Secretariat.

      The syntax of the <addition> element is not specified in this RFC.
      Specific syntax for this element will be specified as needed by
      the ISO Central Secretariat, or by the individual Committee that
      has the responsibility for developing or maintaining the
      identified document.  It is necessary that these definitions
      comply with the rules for lexical equivalence specified in
      Section 2.9 and take into account the process for identifier
      resolution as discussed in Section 2.8.

DIGITS        = DIGIT *DIGIT

DIGIT         = %x30-39 ; 0-9

ALPHA         = %x41-5A / %x61-7A ; A-Z / a-z

NSS           = std-nss

std-nss       = "std:" docidentifier *supplement *docelement
                   [addition]

docidentifier = originator [":" type] ":" docnumber [":" partnumber]
                   [[":" status] ":" edition]
                   [":" docversion] [":" language]

originator    = "iso" / "iso-iec" / "iso-cie" / "iso-astm" /
                     "iso-ieee" / "iec"

                     ; iso      = International Organization for
                                  Standardization

                     ; iec      = International Electrotechnical
                                  Commission (IEC), or Commission
                                  Electrotechnique Internationale

                     ; iso-iec  = jointly developed by ISO and IEC

                     ; iso-cie  = jointly developed by ISO and the
                                  Commission Internationale d'Eclairage
                                  (CIE)

                     ; iso-astm = jointly developed by ISO and the
                                  American Society for Testing and
                                  Materials International (ASTM)

                     ; iso-ieee = jointly developed by ISO and the
                                  Institute for Electrical and
                                  Electronics Engineers (IEEE)

type          = "data" / "guide" / "isp" / "iwa" /
                     "pas" / "r" / "tr" / "ts" / "tta"

                     ; data  = Data (document type no longer published)

                     ; guide = Guide

                     ; isp   = International Standardized Profile

                     ; iwa   = International Workshop Agreement
                     ; pas   = Publicly Available Specification

                     ; r     = Recommendation (document type no longer
                               published)

                     ; tr    = Technical Report

                     ; ts    = Technical Specification

                     ; tta   = Technology Trends Assessment

docnumber     = DIGITS

partnumber    = "-" 1*( DIGIT / ALPHA / "-" )

status        = ( "draft" / "cancelled" ) / stage

                     ; draft     =  document that has not yet been
                                    accepted for publication by
                                    international ballot

                     ; cancelled =  document that has been deleted or
                                    withdrawn

stage         = "stage-" stagecode ["." iteration]

stagecode     = DIGIT DIGIT "."  DIGIT DIGIT

iteration     = "v" DIGITS

edition       = "ed-" DIGITS

docversion    = "v" (simpleversion / isoversion)

simpleversion = DIGITS

                       ; 1 = first version published

                       ; 2 = corrected version published

isoversion    = baseversion *includedsuppl

baseversion   = DIGITS

includedsuppl = "-" suppltype supplnumber [ "." supplversion ]
language      = monolingual / bilingual / trilingual

monolingual   = "en" / "fr" / "ru" / "es" / "ar"

bilingual     = "en,fr" / "en,ru" / "fr,ru"

trilingual    = "en,fr,ru"


supplement    = ":" suppltype ":" supplnumber
                   [":" supplversion ] [":" language ]

suppltype     = "amd" / "cor" / "add"

                     ; amd = Amendment

                     ; cor = Technical Corrigendum

                     ; add = Addendum

supplnumber   = DIGITS

supplversion  = "v" DIGITS

docelement    = ":" ( "clause" / "figure" / "table" / "term" ) ":"
                   elementnumber / elementrange
                   *( "," elementnumber / elementrange )

elementnumber = ( ALPHA / DIGITS ) *( "."  DIGITS )

elementrange  = elementnumber "-" elementnumber

addition      = techdefined / isodefined

techdefined   = ":tech" *techelement

techelement   = ; unspecified

isodefined    = ; unspecified

DIGITS        = DIGIT *DIGIT

DIGIT         = %x30-39 ; 0-9

ALPHA         = %x41-5A / %x61-7A ; A-Z / a-z
