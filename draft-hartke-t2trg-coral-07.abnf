body = [*(link / form / representation / directive)]

link = [link: 2, relation, link-target, ?body]

relation = text

link-target = ciri / literal

ciri = <Defined in Section X of RFC XXXX>

literal = bool / int / float / time / bytes / text / null
form = [form: 3, relation, method, submission-target, ?form-
method = text / uint

submission-target = ciri

form-fields = [*(form-field-name, form-field-value)]

form-field-name = text

form-field-value = ciri / literal

representation = [representation: 0, text / uint, bytes]

directive = base-directive

base-directive = [base: -1, base]

base = ciri

token = identifier / iri / iriref / literal / punctuator

identifier = START *CONTINUE *(MEDIAL 1*CONTINUE)

START = <Any character with the XID_Start property>

CONTINUE = <Any character with the XID_Continue property>

MEDIAL = "-" / "." / "~" / %x58A / %xF0B

MEDIAL =/ %x2010 / %x2027 / %x30A0 / %x30FB

iri = "<" IRI ">"

iriref = "<" IRI-reference ">"

IRI = <Defined in Section 2.2 of RFC 3987>

IRI-reference = <Defined in Section 2.2 of RFC 3987>

literal = boolean / integer / float / datetime / bytes / text

literal =/ null

boolean = "true" / "false"

integer = ["+" / "-"] (decimal / binary / octal / hexadecimal)
decimal = 1*DIGIT

binary = %x30 (%x42 / %x62) 1*BINDIG

octal = %x30 (%x4F / %x6F) 1*OCTDIG

hexadecimal = %x30 (%x58 / %x78) 1*HEXDIG

DIGIT = %x30-39

BINDIG = %x30-31

OCTDIG = %x30-37

HEXDIG = %x30-39 / %x41-46 / %x61-66

float = ["+" / "-"] 1*DIGIT [fraction] [exponent]

fraction = "." 1*DIGIT

exponent = (%x45 / %x65) ["+" / "-"] 1*DIGIT

float =/ "NaN"

float =/ ["+" / "-"] "Infinity"
datetime = DOLLAR date-time DOLLAR

date-time = <Defined in Section 5.6 of RFC 3339>

DOLLAR = %x24

bytes = base16 / base32 / base64

base16 = (%x68 / %x62.31.36) SQUOTE <Base16 encoded data> SQUOTE

base32 = %x62.33.32 SQUOTE <Base32 encoded data> SQUOTE

base64 = %x62.36.34 SQUOTE <Base64 encoded data> SQUOTE

SQUOTE = %x27

text = DQUOTE *(char / %x5C escape) DQUOTE

char = <Any character except %x22, %x5C, and line terminators>

escape = simple-escape / hexadecimal-escape / unicode-escape
simple-escape = %x30 / %x62 / %x74 / %x6E / %x76

simple-escape =/ %x66 / %x72 / %x22 / %x27 / %x5C

hexadecimal-escape = (%x78 / %x58) 2HEXDIG

unicode-escape = %x75 4HEXDIG / %x55 8HEXDIG

DQUOTE = %x22

null = "null" / "_"

punctuator = "#" / ":" / "*" / "[" / "]" / "{" / "}" / "=" / "->"
body = *(link / form / representation / directive)

link = relation link-target ["{" body "}"]

relation = iri / simple-name / qualified-name

simple-name = identifier

qualified-name = identifier ":" identifier

link-target = iriref / literal

form = relation "->" method submission-target ["[" form-fields
method = identifier

submission-target = iriref
form-fields = *(form-field-name form-field-value)

form-field-name = iri / simple-name / qualified-name

form-field-value = iriref / literal

representation = "*" bytes ["[" representation-metadata "]"]

representation-metadata = *(metadata-name metadata-value)

metadata-name = "type"

metadata-value = text / integer

directive = base-directive / using-directive

base-directive = "#" "base" base

base = iriref

using-directive = "#" "using" [identifier "="] iri

ct=51; sz=247; obs

CoRAL-Document = body

CoRAL-Link = link

CoRAL-Form = form

