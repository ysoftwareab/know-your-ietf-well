keystring = leadkeychar *keychar
leadkeychar = ALPHA
keychar = ALPHA / DIGIT / HYPHEN
number  = DIGIT / ( LDIGIT 1*DIGIT )

ALPHA   = %x41-5A / %x61-7A   ; "A"-"Z" / "a"-"z"
DIGIT   = %x30 / LDIGIT       ; "0"-"9"
LDIGIT  = %x31-39             ; "1"-"9"
HEX     = DIGIT / %x41-46 / %x61-66 ; "0"-"9" / "A"-"F" / "a"-"f"

SP      = 1*SPACE  ; one or more " "
WSP     = 0*SPACE  ; zero or more " "

NULL    = %x00 ; null (0)
SPACE   = %x20 ; space (" ")
DQUOTE  = %x22 ; quote (""")
SHARP   = %x23 ; octothorpe (or sharp sign) ("#")
DOLLAR  = %x24 ; dollar sign ("$")
SQUOTE  = %x27 ; single quote ("'")
LPAREN  = %x28 ; left paren ("(")
RPAREN  = %x29 ; right paren (")")
PLUS    = %x2B ; plus sign ("+")
COMMA   = %x2C ; comma (",")
HYPHEN  = %x2D ; hyphen ("-")
DOT     = %x2E ; period (".")
SEMI    = %x3B ; semicolon (";")
LANGLE  = %x3C ; left angle bracket ("<")
EQUALS  = %x3D ; equals sign ("=")
RANGLE  = %x3E ; right angle bracket (">")
ESC     = %x5C ; backslash ("\")
USCORE  = %x5F ; underscore ("_")
LCURLY  = %x7B ; left curly brace "{"
RCURLY  = %x7D ; right curly brace "}"

UTF8    = UTF1 / UTFMB
UTFMB   = UTF2 / UTF3 / UTF4
UTF0    = %x80-BF
UTF1    = %x00-7F
UTF2    = %xC2-DF UTF0
UTF3    = %xE0 %xA0-BF UTF0 / %xE1-EC 2(UTF0) /
                %xED %x80-9F UTF0 / %xEE-EF 2(UTF0)
UTF4    = %xF0 %x90-BF 2(UTF0) / %xF1-F3 3(UTF0) /
                %xF4 %x80-8F 2(UTF0)

OCTET   = %x00-FF ; Any octet (8-bit data unit)

numericoid = number 1*( DOT number )

descr = keystring

oid = descr / numericoid

UID=12345
OU=Engineering
CN=Kurt Zeilenga+L=Redwood Shores

UID=nobody@example.com,DC=example,DC=com
CN=John Smith,OU=Sales,O=ACME Limited,L=Moab,ST=Utah,C=US


attributedescription = attributetype options
attributetype = oid
options = *( SEMI option )
option = 1*keychar

noidlen = numericoid [ LCURLY len RCURLY ]
len = number

oids = oid / ( LPAREN WSP oidlist WSP RPAREN )
oidlist = oid *( WSP DOLLAR WSP oid )

extensions = *( SP xstring SP qdstrings )
xstring = "X" HYPHEN 1*( ALPHA / HYPHEN / USCORE )

qdescrs = qdescr / ( LPAREN WSP qdescrlist WSP RPAREN )
qdescrlist = [ qdescr *( SP qdescr ) ]
qdescr = SQUOTE descr SQUOTE

qdstrings = qdstring / ( LPAREN WSP qdstringlist WSP RPAREN )
qdstringlist = [ qdstring *( SP qdstring ) ]
qdstring = SQUOTE dstring SQUOTE
dstring = 1*( QS / QQ / QUTF8 )   ; escaped UTF-8 string
QQ =  ESC %x32 %x37 ; "\27"
QS =  ESC %x35 ( %x43 / %x63 ) ; "\5C" / "\5c"

QUTF8    = QUTF1 / UTFMB

QUTF1    = %x00-26 / %x28-5B / %x5D-7F

ObjectClassDescription = LPAREN WSP
        numericoid                 ; object identifier
        [ SP "NAME" SP qdescrs ]   ; short names (descriptors)
        [ SP "DESC" SP qdstring ]  ; description
        [ SP "OBSOLETE" ]          ; not active
        [ SP "SUP" SP oids ]       ; superior object classes
        [ SP kind ]                ; kind of class
        [ SP "MUST" SP oids ]      ; attribute types
        [ SP "MAY" SP oids ]       ; attribute types
        extensions WSP RPAREN

kind = "ABSTRACT" / "STRUCTURAL" / "AUXILIARY"

AttributeTypeDescription = LPAREN WSP
        numericoid                    ; object identifier
        [ SP "NAME" SP qdescrs ]      ; short names (descriptors)
        [ SP "DESC" SP qdstring ]     ; description
        [ SP "OBSOLETE" ]             ; not active
        [ SP "SUP" SP oid ]           ; supertype
        [ SP "EQUALITY" SP oid ]      ; equality matching rule
        [ SP "ORDERING" SP oid ]      ; ordering matching rule
        [ SP "SUBSTR" SP oid ]        ; substrings matching rule
        [ SP "SYNTAX" SP noidlen ]    ; value syntax
        [ SP "SINGLE-VALUE" ]         ; single-value
        [ SP "COLLECTIVE" ]           ; collective
        [ SP "NO-USER-MODIFICATION" ] ; not user modifiable
        [ SP "USAGE" SP usage ]       ; usage
        extensions WSP RPAREN         ; extensions

usage = "userApplications"     /  ; user
            "directoryOperation"   /  ; directory operational
            "distributedOperation" /  ; DSA-shared operational
            "dSAOperation"            ; DSA-specific operational

MatchingRuleDescription = LPAREN WSP
        numericoid                 ; object identifier
        [ SP "NAME" SP qdescrs ]   ; short names (descriptors)
        [ SP "DESC" SP qdstring ]  ; description
        [ SP "OBSOLETE" ]          ; not active
        SP "SYNTAX" SP numericoid  ; assertion syntax
        extensions WSP RPAREN      ; extensions

MatchingRuleUseDescription = LPAREN WSP
        numericoid                 ; object identifier
        [ SP "NAME" SP qdescrs ]   ; short names (descriptors)
        [ SP "DESC" SP qdstring ]  ; description
        [ SP "OBSOLETE" ]          ; not active
        SP "APPLIES" SP oids       ; attribute types
        extensions WSP RPAREN      ; extensions

SyntaxDescription = LPAREN WSP
        numericoid                 ; object identifier
        [ SP "DESC" SP qdstring ]  ; description
        extensions WSP RPAREN      ; extensions

DITContentRuleDescription = LPAREN WSP
        numericoid                 ; object identifier
        [ SP "NAME" SP qdescrs ]   ; short names (descriptors)
        [ SP "DESC" SP qdstring ]  ; description
        [ SP "OBSOLETE" ]          ; not active
        [ SP "AUX" SP oids ]       ; auxiliary object classes
        [ SP "MUST" SP oids ]      ; attribute types
        [ SP "MAY" SP oids ]       ; attribute types
        [ SP "NOT" SP oids ]       ; attribute types
        extensions WSP RPAREN      ; extensions

DITStructureRuleDescription = LPAREN WSP
        ruleid                     ; rule identifier
        [ SP "NAME" SP qdescrs ]   ; short names (descriptors)
        [ SP "DESC" SP qdstring ]  ; description
        [ SP "OBSOLETE" ]          ; not active
        SP "FORM" SP oid           ; NameForm
        [ SP "SUP" ruleids ]       ; superior rules
        extensions WSP RPAREN      ; extensions

ruleids = ruleid / ( LPAREN WSP ruleidlist WSP RPAREN )
ruleidlist = ruleid *( SP ruleid )
ruleid = number
NameFormDescription = LPAREN WSP
        numericoid                 ; object identifier
        [ SP "NAME" SP qdescrs ]   ; short names (descriptors)
        [ SP "DESC" SP qdstring ]  ; description
        [ SP "OBSOLETE" ]          ; not active
        SP "OC" SP oid             ; structural object class
        SP "MUST" SP oids          ; attribute types
        [ SP "MAY" SP oids ]       ; attribute types
        extensions WSP RPAREN      ; extensions

