availabilityc  = "BEGIN" ":" "VAVAILABILITY" CRLF
                   availabilityprop *availablec
                   "END" ":" "VAVAILABILITY" CRLF

availabilityprop  = *(
                    ;
                    ; the following are REQUIRED
                    ; but MUST NOT occur more than once
                    ;
                    dtstamp / uid
                    ;
                    ; the following are OPTIONAL
                    ; but MUST NOT occur more than once
                    ;
                    busytype / class / created / description /
                    dtstart / last-mod / location / organizer /
                    priority /seq / summary / url /
                    ;
                    ; Either 'dtend' or 'duration' MAY appear
                    ; in an 'availableprop', but 'dtend' and
                    ; 'duration' MUST NOT occur in the same
                    ; 'availabilityprop'.
                    ; 'duration' MUST NOT be present if
                    ; 'dtstart' is not present
                    ;
                    dtend / duration /
                    ;
                    ; the following are OPTIONAL
                    ; and MAY occur more than once
                    ;
                    categories / comment / contact /
                    x-prop / iana-prop
                    ;
                    )

availablec  = "BEGIN" ":" "AVAILABLE" CRLF
                availableprop
                "END" ":" "AVAILABLE" CRLF
availableprop  = *(
                 ;
                 ; the following are REQUIRED
                 ; but MUST NOT occur more than once
                 ;
                 dtstamp / dtstart / uid /
                 ;
                 ; Either 'dtend' or 'duration' MAY appear in
                 ; an 'availableprop', but 'dtend' and
                 ; 'duration' MUST NOT occur in the same
                 ; 'availableprop'.
                 ;
                 dtend / duration /
                 ;
                 ; the following are OPTIONAL
                 ; but MUST NOT occur more than once
                 ;
                 created / description / last-mod /
                 location / recurid / rrule / summary /
                 ;
                 ; the following are OPTIONAL
                 ; and MAY occur more than once
                 ;
                 categories / comment / contact / exdate /
                 rdate / x-prop / iana-prop
                 ;
                 )

busytype      = "BUSYTYPE" busytypeparam ":" busytypevalue CRLF

busytypeparam = *(";" other-param)

busytypevalue = "BUSY" / "BUSY-UNAVAILABLE" /
                   "BUSY-TENTATIVE" / iana-token / x-name
                  ; Default is "BUSY-UNAVAILABLE".

