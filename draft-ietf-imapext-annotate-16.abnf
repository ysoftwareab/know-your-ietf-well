ann-size          = "NONE" /
                           (("READ-ONLY" / nz-size)
                            [SP "NOPRIVATE"])
                           ; response codes indicating the level of
                           ; support for annotations in a mailbox

append-ext        =/ att-annotate
                          ; modifies [RFC3501] extension behaviour

att-annotate      = "ANNOTATION" SP
                               "(" entry-att *(SP entry-att) ")"

att-search        = "value" / "value.priv" / "value.shared"
                          ; the only attributes that can be searched
att-sort          = "value.priv" / "value.shared"
                          ; the only attributes that can be sorted

att-value         = attrib SP value

attrib            = astring
                          ; dot-separated attribute name
                          ; MUST NOT contain "*" or "%"

attribs           = attrib / "(" attrib *(SP attrib) ")"
                          ; one or more attribute specifiers

capability        =/ "ANNOTATE-EXPERIMENT-1"
                          ; defines the capability for this extension

entries           = entry-match /
                          "(" entry-match *(SP entry-match) ")"

entry             = astring
                          ; slash-separated path to entry
                          ; MUST NOT contain "*" or "%"

entry-att         = entry SP "(" att-value *(SP att-value) ")"

entry-match       = list-mailbox
                          ; slash-separated path to entry
                          ; MAY contain "*" or "%" for use as wild cards

fetch-att         =/ "ANNOTATION" SP "(" entries SP attribs ")"
                          ; modifies original IMAP fetch-att

msg-att-dynamic   =/ "ANNOTATION" SP
                             ( "(" entry-att *(SP entry-att) ")" /
                               "(" entry *(SP entry) ")" )
                          ; extends FETCH response with annotation data

resp-text-code    =/ "ANNOTATE" SP "TOOBIG" /
                           "ANNOTATE" SP "TOOMANY" /
                           "ANNOTATIONS" SP ann-size
                          ; new response codes

search-key        =/ "ANNOTATION" SP entry-match SP att-search
                           SP value
                          ; modifies original IMAP search-key

select-param      =/ "ANNOTATE"
                          ; defines the select parameter used with
                          ; ANNOTATE extension
sort-key          =/ "ANNOTATION" SP entry SP att-sort
                          ; modifies original sort-key

store-att-flags   =/ att-annotate
                          ; modifies original IMAP STORE command

value             = nstring / literal8



