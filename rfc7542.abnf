UTF8-xtra-char  =   UTF8-2 / UTF8-3 / UTF8-4

UTF8-2          =   %xC2-DF UTF8-tail

UTF8-3          =   %xE0 %xA0-BF UTF8-tail /
                       %xE1-EC 2( UTF8-tail ) /
                       %xED %x80-9F UTF8-tail /
                       %xEE-EF 2( UTF8-tail )

UTF8-4          =   %xF0 %x90-BF 2( UTF8-tail ) /
                       %xF1-F3 3( UTF8-tail ) /
                       %xF4 %x80-8F 2( UTF8-tail )

UTF8-tail       =   %x80-BF

nai            =   utf8-username
nai            =/  "@" utf8-realm
nai            =/  utf8-username "@" utf8-realm

utf8-username  =  dot-string

dot-string     = string *("." string)
string         = 1*utf8-atext

utf8-atext     =  ALPHA / DIGIT /
                     "!" / "#" /
                     "$" / "%" /
                     "&" / "'" /
                     "*" / "+" /
                     "-" / "/" /
                     "=" / "?" /
                     "^" / "_" /
                     "`" / "{" /
                     "|" / "}" /
                     "~" /
                     UTF8-xtra-char

utf8-realm     =  1*( label "." ) label

label          =  utf8-rtext *(ldh-str)
ldh-str        =  *( utf8-rtext / "-" ) utf8-rtext
utf8-rtext     =  ALPHA / DIGIT / UTF8-xtra-char

fred=?#$&*+-/^smith@example.com
