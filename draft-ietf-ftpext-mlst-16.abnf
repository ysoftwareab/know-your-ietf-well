TCHAR          = VCHAR / SP / HTAB    ; visible plus white space
RCHAR          = ALPHA / DIGIT / "," / "." / ":" / "!" /
                         "@" / "#" / "$" / "%" / "^" /
                         "&" / "(" / ")" / "-" / "_" /
                         "+" / "?" / "/" / "\" / "'" /
                         DQUOTE   ; <"> -- double quote character (%x22)
SCHAR          = RCHAR / "=" ;

token          = 1*RCHAR

pathname       = utf-8-name / raw
utf-8-name     = <a UTF-8 encoded Unicode string>
raw            = <any string not being a valid UTF-8 encoding>

time-val       = 14DIGIT [ "." 1*DIGIT ]

error-response = error-code SP *TCHAR CRLF
error-code     = ("4" / "5") 2DIGIT

mdtm          = "MdTm" SP pathname CRLF

mdtm-response = "213" SP time-val CRLF /
                        error-response

size          = "Size" SP pathname CRLF

size-response = "213" SP 1*DIGIT CRLF /
                        error-response

rest          = "Rest" SP 1*DIGIT CRLF

rest-response = "350" SP *TCHAR CRLF /
                        error-response

mlst             = "MLst" [ SP pathname ] CRLF
mlsd             = "MLsD" [ SP pathname ] CRLF

mlst-response    = control-response / error-response
mlsd-response    = ( initial-response final-response ) /
                           error-response

control-response = "250-" [ response-message ] CRLF
                           1*( SP entry CRLF )
                           "250" [ SP response-message ] CRLF

initial-response = "150" [ SP response-message ] CRLF
final-response   = "226" SP response-message CRLF

response-message = *TCHAR

data-response    = *( entry CRLF )

entry            = [ facts ] SP pathname
facts            = 1*( fact ";" )
fact             = factname "=" value
factname         = "Size" / "Modify" / "Create" /
                           "Type" / "Unique" / "Perm" /
                           "Lang" / "Media-Type" / "CharSet" /
                           os-depend-fact / local-fact
os-depend-fact   = <IANA assigned OS name> "." token
local-fact       = "X." token
value            = *SCHAR

size=4161;lang=en-US;modify=19970214165800;create=19961001124534;
type=file;x.myfact=foo,bar;

type-fact       = type-label "=" type-val
type-label      = "Type"
type-val        = "File" / "cdir" / "pdir" / "dir" /
                          os-type

os-type   = "OS." os-name "=" os-kind
os-name   = <an IANA registered operating system name>
os-kind   = token

unique-fact  = "Unique" "=" token

modify-fact  = "Modify" "=" time-val

create-fact  = "Create" "=" time-val

perm-fact    = "Perm" "=" *pvals
pvals        = "a" / "c" / "d" / "e" / "f" /
                       "l" / "m" / "p" / "r" / "w"

type=cdir).  It indicates that files may be created in the directory
lang-fact  = "Lang" "=" token

size-fact  = "Size" "=" 1*DIGIT

media-type  = "Media-Type" "=" <per IANA guidelines>

charset-type  = "Charset" "=" token

mlst-feat     = SP "MLST" [SP factlist] CRLF
factlist      = 1*( factname ["*"] ";" )

mlst-opts     = "OPTS" SP "MLST"
                        [ SP 1*( factname ";" ) ]

mlst-opt-resp = "MLST OPTS" [ SP 1*( factname ";" ) ]

