LOWER-ALPHA     =  %x61-7A   ;; a-z

acl-data        = "ACL" SP mailbox *(SP identifier SP
                       rights)

capability      =/ rights-capa
                       ;;capability is defined in [IMAP4]

command-auth    =/ setacl / deleteacl / getacl /
                       listrights / myrights
                       ;;command-auth is defined in [IMAP4]

deleteacl       = "DELETEACL" SP mailbox SP identifier

getacl          = "GETACL" SP mailbox

identifier      = astring

listrights      = "LISTRIGHTS" SP mailbox SP identifier

listrights-data = "LISTRIGHTS" SP mailbox SP identifier
                           SP rights *(SP rights)

mailbox-data    =/ acl-data / listrights-data / myrights-data
                       ;;mailbox-data is defined in [IMAP4]

mod-rights      = astring
                       ;; +rights to add, -rights to remove
                       ;; rights to replace

myrights        = "MYRIGHTS" SP mailbox

myrights-data   = "MYRIGHTS" SP mailbox SP rights

new-rights      = 1*LOWER-ALPHA
                       ;; MUST include "t", "e", "x" and "k".
                       ;; MUST NOT include standard rights listed
                       ;; in section 2.2

rights          = astring
                       ;; only lowercase ASCII letters and digits
                       ;; are allowed.

rights-capa     = "RIGHTS=" new-rights
                       ;; RIGHTS=... capability

setacl          = "SETACL" SP mailbox SP identifier
                       SP mod-rights
