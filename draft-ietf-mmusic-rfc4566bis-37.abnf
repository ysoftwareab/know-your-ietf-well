v=  (protocol version)
o=  (originator and session identifier)
s=  (session name)
i=* (session information)
u=* (URI of description)
e=* (email address)
p=* (phone number)
c=* (connection information -- not required if included in
              all media descriptions)
b=* (zero or more bandwidth information lines)
k=* (obsolete)
a=* (zero or more session attribute lines)
t=  (time the session is active)
r=* (zero or more repeat times)
z=* (optional time zone offset line)

m=  (media name and transport address)
i=* (media title)
c=* (connection information -- optional if included at
              session level)
b=* (zero or more bandwidth information lines)
k=* (obsolete)
a=* (zero or more media attribute lines)

v=0
o=jdoe 3724394400 3724394405 IN IP4 198.51.100.1
s=Call to John Smith
i=SDP Offer #1
u=http://www.jdoe.example.com/home.html
e=Jane Doe <jane@jdoe.example.com>
p=+1 617 555-6011
c=IN IP4 198.51.100.1
t=0 0
m=audio 49170 RTP/AVP 0
m=audio 49180 RTP/AVP 0
m=video 51372 RTP/AVP 99
c=IN IP6 2001:db8::2
a=rtpmap:99 h263-1998/90000

v=0

o=<username> <sess-id> <sess-version> <nettype> <addrtype>
        <unicast-address>

s=<session name>

i=<session information>

u=<uri>

e=<email-address>
p=<phone-number>

p=+1 617 555-6011

e=j.doe@example.com (Jane Doe)

e=Jane Doe <j.doe@example.com>
c=<nettype> <addrtype> <connection-address>

c=IN IP4 233.252.0.1/127

c=IN IP4 233.252.0.1/127/3

c=IN IP4 233.252.0.1/127
c=IN IP4 233.252.0.2/127
c=IN IP4 233.252.0.3/127

c=IN IP6 ff00::db8:0:101/3

c=IN IP6 ff00::db8:0:101
c=IN IP6 ff00::db8:0:102
c=IN IP6 ff00::db8:0:103

b=<bwtype>:<bandwidth>

b=X-YZ:128

t=<start-time> <stop-time>

t=3724394400 3724398000 ; Mon 8-Jan-2018 10:00-11:00 UTC
t=3724484400 3724488000 ; Tue 9-Jan-2018 11:00-12:00 UTC

r=<repeat interval> <active duration> <offsets from start-time>

t=3724394400 3730536000 ; Mon 8-Jan-2018 10:00-11:00 UTC
                              ; Tues 20-Mar-2018 12:00 UTC
r=604800 3600 0 90000   ; 1 week, 1 hour, zero, 25 hours

r=7d 1h 0 25h

z=<adjustment time> <offset> <adjustment time> <offset> ....

t=3724394400 3754123200       ; Mon 8-Jan-2018 10:00 UTC
                                    ; Tues 18-Dec-2018 12:00 UTC
r=604800 3600 0 90000         ; 1 week, 1 hour, zero, 25 hours
z=3730928400 -1h 3749680800 0 ; Sun 25-Mar-2018 1:00 UTC,
                                    ; offset 1 hour,
                                    ; Sun 28-Oct-2018 2:00 UTC,
                                    ; no offset

k=<method>
k=<method>:<encryption key>

a=<attribute>
a=<attribute>:<value>

m=<media> <port> <proto> <fmt> ...

m=<media> <port>/<number of ports> <proto> <fmt> ...

m=video 49170/2 RTP/AVP 31

m=video 49170/2 RTP/AVP 31
c=IN IP4 233.252.0.1/127/2

m=video 49170/2 RTP/AVP 31
c=IN IP6 ff00::db8:0:101
c=IN IP6 ff00::db8:0:102

cat-value = category
category = non-ws-string

a=cat:foo.bar

keywds-value = keywords
keywords = text

a=keywds:SDP session description protocol

tool-value = tool-name-and-version
tool-name-and-version = text

a=tool:foobar V3.2

ptime-value = non-zero-int-or-real

a=ptime:20

maxptime-value = non-zero-int-or-real

a=maxptime:20

rtpmap-value = payload-type SP encoding-name
           "/" clock-rate [ "/" encoding-params ]
payload-type = zero-based-integer
encoding-name = token
clock-rate = integer
encoding-params = channels
channels = integer

m=audio 49232 RTP/AVP 0

m=audio 49232 RTP/AVP 98
a=rtpmap:98 L16/16000/2

m=audio 49230 RTP/AVP 96 97 98
a=rtpmap:96 L8/8000
a=rtpmap:97 L16/8000
a=rtpmap:98 L16/11025/2

v=0
o=jdoe 3724395000 3724395001 IN IP6 2001:db8::1
s=-
c=IN IP6 2001:db8::1
t=0 0
a=inactive
m=audio 49170 RTP/AVP 0
a=sendrecv
m=audio 49180 RTP/AVP 0
m=video 51372 RTP/AVP 99
a=rtpmap:99 h263-1998/90000

a=recvonly

a=sendrecv
a=sendonly

a=inactive

orient-value = portrait / landscape / seascape
portrait  = %s"portrait"
landscape = %s"landscape"
seascape  = %s"seascape"
            ; NOTE: These names are case-sensitive.

a=orient:portrait

type-value = conference-type
conference-type = broadcast / meeting / moderated / test /
                               H332
broadcast = %s"broadcast"
meeting   = %s"meeting"
moderated = %s"moderated"
test      = %s"test"
H332      = %s"H332"
                ; NOTE: These names are case-sensitive.
a=type:moderated

charset-value = <defined in [RFC2978]>

a=charset:ISO-8859-1

sdplang-value = Language-Tag
a=sdplang:fr

lang-value = Language-Tag
a=lang:de

framerate-value = non-zero-int-or-real

a=framerate:60

quality-value = zero-based-integer

a=quality:10

fmtp-value = fmt SP format-specific-params
format-specific-params = byte-string
           ; Notes:
           ; - The format parameters are media type parameters and
           ;   need to reflect their syntax.

a=fmtp:96 profile-level-id=42e016;max-mbps=108000;max-fs=3600

session-description = version-field
                         origin-field
                         session-name-field
                         [information-field]
                         [uri-field]
                         *email-field
                         *phone-field
                         [connection-field]
                         *bandwidth-field
                         1*time-description
                         [key-field]
                         *attribute-field
                         *media-description

version-field =       %s"v" "=" 1*DIGIT CRLF
                             ;this memo describes version 0

origin-field =       %s"o" "=" username SP sess-id SP sess-version SP
                            nettype SP addrtype SP unicast-address CRLF

session-name-field =  %s"s" "=" text CRLF

information-field =   %s"i" "=" text CRLF

uri-field =           %s"u" "=" uri CRLF

email-field =         %s"e" "=" email-address CRLF

phone-field =         %s"p" "=" phone-number CRLF

connection-field =    %s"c" "=" nettype SP addrtype SP
                             connection-address CRLF
                             ;a connection field must be present
                             ;in every media description or at the
                             ;session level
bandwidth-field =     %s"b" "=" bwtype ":" bandwidth CRLF

time-description =    time-field
                             [repeat-description]

repeat-description =  1*repeat-field
                             [zone-field]

time-field =          %s"t" "=" start-time SP stop-time CRLF

repeat-field =        %s"r" "=" repeat-interval SP typed-time
                             1*(SP typed-time) CRLF

zone-field =          %s"z" "=" time SP ["-"] typed-time
                             *(SP time SP ["-"] typed-time) CRLF

key-field =           %s"k" "=" key-type CRLF

attribute-field =     %s"a" "=" attribute CRLF

media-description =   media-field
                         [information-field]
                         *connection-field
                         *bandwidth-field
                         [key-field]
                         *attribute-field

media-field =         %s"m" "=" media SP port ["/" integer]
                             SP proto 1*(SP fmt) CRLF

username =            non-ws-string
                         ;pretty wide definition, but doesn't
                         ;include space

sess-id =             1*DIGIT
                         ;should be unique for this username/host

sess-version =        1*DIGIT

nettype =             token
                         ;typically "IN"

addrtype =            token
                         ;typically "IP4" or "IP6"

uri =                 URI-reference
                         ; see RFC 3986

email-address        = address-and-comment / dispname-and-address
                          / addr-spec
address-and-comment  = addr-spec 1*SP "(" 1*email-safe ")"
dispname-and-address = 1*email-safe 1*SP "<" addr-spec ">"

phone-number =        phone *SP "(" 1*email-safe ")" /
                         1*email-safe "<" phone ">" /
                         phone

phone =               ["+"] DIGIT 1*(SP / "-" / DIGIT)

connection-address =  multicast-address / unicast-address

bwtype =              token

bandwidth =           1*DIGIT

start-time =          time / "0"

stop-time =           time / "0"

time =                POS-DIGIT 9*DIGIT
                         ; Decimal representation of time in
                         ; seconds since January 1, 1900 UTC.
                         ; The representation is an unbounded
                         ; length field containing at least
                         ; 10 digits. Unlike some representations
                         ; used elsewhere, time in SDP does not
                         ; wrap in the year 2036.

repeat-interval =     POS-DIGIT *DIGIT [fixed-len-time-unit]

typed-time =          1*DIGIT [fixed-len-time-unit]

fixed-len-time-unit = %s"d" / %s"h" / %s"m" / %s"s"
key-type =            %s"prompt" /
                         %s"clear:" text /
                         %s"base64:" base64 /
                         %s"uri:" uri
                         ; NOTE: These names are case-sensitive.

base64      =         *base64-unit [base64-pad]
base64-unit =         4base64-char
base64-pad  =         2base64-char "==" / 3base64-char "="
base64-char =         ALPHA / DIGIT / "+" / "/"

attribute =           (att-field ":" att-value) / att-field

att-field =           token

att-value =           byte-string

media =               token
                         ;typically "audio", "video", "text", "image"
                         ;or "application"

fmt =                 token
                         ;typically an RTP payload type for audio
                         ;and video media

proto  =              token *("/" token)
                         ;typically "RTP/AVP" or "udp"

port =                1*DIGIT

unicast-address =     IP4-address / IP6-address / FQDN / extn-addr

multicast-address =   IP4-multicast / IP6-multicast / FQDN
                         / extn-addr

IP4-multicast =       m1 3( "." decimal-uchar )
                         "/" ttl [ "/" numaddr ]
                         ; IP4 multicast addresses may be in the
                         ; range 224.0.0.0 to 239.255.255.255

m1 =                  ("22" ("4"/"5"/"6"/"7"/"8"/"9")) /
                         ("23" DIGIT )

IP6-multicast =       IP6-address [ "/" numaddr ]
                         ; IP6 address starting with FF

numaddr =             integer
ttl =                 (POS-DIGIT *2DIGIT) / "0"

FQDN =                4*(alpha-numeric / "-" / ".")
                         ; fully qualified domain name as specified
                         ; in RFC 1035 (and updates)

IP4-address =         b1 3("." decimal-uchar)

b1 =                  decimal-uchar
                         ; less than "224"

IP6-address =                                      6( h16 ":" ) ls32
                         /                       "::" 5( h16 ":" ) ls32
                         / [               h16 ] "::" 4( h16 ":" ) ls32
                         / [ *1( h16 ":" ) h16 ] "::" 3( h16 ":" ) ls32
                         / [ *2( h16 ":" ) h16 ] "::" 2( h16 ":" ) ls32
                         / [ *3( h16 ":" ) h16 ] "::"    h16 ":"   ls32
                         / [ *4( h16 ":" ) h16 ] "::"              ls32
                         / [ *5( h16 ":" ) h16 ] "::"              h16
                         / [ *6( h16 ":" ) h16 ] "::"

h16 =                 1*4HEXDIG

ls32 =                ( h16 ":" h16 ) / IP4-address

extn-addr =      non-ws-string

text =                byte-string
                         ;default is to interpret this as UTF8 text.
                         ;ISO 8859-1 requires "a=charset:ISO-8859-1"
                         ;session-level attribute to be used

byte-string =         1*(%x01-09/%x0B-0C/%x0E-FF)
                         ;any byte except NUL, CR, or LF

non-ws-string =       1*(VCHAR/%x80-FF)
                         ;string of visible characters

token-char =          ALPHA / DIGIT
                                 / "!" / "#" / "$" / "%" / "&"
                                 / "'" ; (single quote)
                                 / "*" / "+" / "-" / "." / "^" / "_"
                                 / "`" ; (Grave accent)
                                 / "{" / "|" / "}" / "~"

token =               1*(token-char)
email-safe =          %x01-09/%x0B-0C/%x0E-27/%x2A-3B/%x3D/%x3F-FF
                         ;any byte except NUL, CR, LF, or the quoting
                         ;characters ()<>

integer =             POS-DIGIT *DIGIT

zero-based-integer = "0" / integer

non-zero-int-or-real = integer / non-zero-real

non-zero-real = zero-based-integer "." *DIGIT POS-DIGIT

alpha-numeric =       ALPHA / DIGIT

POS-DIGIT =           %x31-39 ; 1 - 9

decimal-uchar =       DIGIT
                         / POS-DIGIT DIGIT
                         / ("1" 2(DIGIT))
                         / ("2" ("0"/"1"/"2"/"3"/"4") DIGIT)
                         / ("2" "5" ("0"/"1"/"2"/"3"/"4"/"5"))

ALPHA =               <ALPHA definition from RFC5234>
DIGIT =               <DIGIT definition from RFC5234>
CRLF =                <CRLF definition from RFC5234>
HEXDIG =              <HEXDIG definition from RFC5234>
SP =                  <SP definition from RFC5234>
VCHAR =               <VCHAR definition from RFC5234>
URI-reference =       <URI-reference definition from RFC3986>
addr-spec =           <addr-spec definition from RFC5322>

