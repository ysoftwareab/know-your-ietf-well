UTF8-xtra-char  =   UTF8-2 / UTF8-3 / UTF8-4

UTF8-2          =   %xC2-DF UTF8-tail

UTF8-3          =   %xE0 %xA0-BF UTF8-tail /
                       %xE1-EC 2(UTF8-tail) /
                       %xED %x80-9F UTF8-tail /
                       %xEE-EF 2(UTF8-tail)

UTF8-4          =   %xF0 %x90-BF 2( UTF8-tail ) /
                       %xF1-F3 3( UTF8-tail ) /
                       %xF4 %x80-8F 2( UTF8-tail )

UTF8-tail       =   %x80-BF
FWS     =  <see [RFC 2822], folding white space>

CFWS    =  <see [RFC 2822], folding white space>

ctext   =/  UTF8-xtra-char

utext   =/  UTF8-xtra-char

comment = "(" *([FWS] utf8-ccontent) [FWS] ")"

word    = utf8-atom / utf8-quoted-string


utf8-text   =  %d1-9 /         ; all UTF-8 characters except
              %d11-12 /       ; US-ASCII NUL, CR and LF
              %d14-127 /
              UTF8-xtra-char

utf8-quoted-pair   = ("\" utf8-text) / obs-qp

utf8-qcontent      = utf8-qtext / utf8-quoted-pair

utf8-quoted-string = [CFWS]
                           DQUOTE *([FWS] utf8-qcontent) [FWS] DQUOTE
                           [CFWS]

utf8-ccontent = ctext / utf8-quoted-pair / comment
utf8-qtext= qtext / UTF8-xtra-char


utf8-atext   =  ALPHA / DIGIT /
                   "!" / "#" /     ; Any character except
                   "$" / "%" /     ; controls, SP, and specials.
                   "&" / "'" /     ; Used for atoms
                   "*" / "+" /
                   "-" / "/" /
                   "=" / "?" /
                   "^" / "_" /
                   "`" / "{" /
                   "|" / "}" /
                   "~" /
                   UTF8-xtra-char

utf8-atom     = [CFWS] 1*utf8-atext [CFWS]

utf8-dot-atom = [CFWS] utf8-dot-atom-text [CFWS]

utf8-dot-atom-text = 1*utf8-atext *("." 1*utf8-atext)

qcontent      = utf8-qcontent

description   = "Content-Description:" unstructured CRLF
mailbox        =  name-addr / addr-spec / utf8-addr-spec

angle-addr     =/ [CFWS] "<" utf8-addr-spec [ alt-address ] ">" [CFWS] /
                  obs-angle-addr

utf8-addr-spec =  utf8-local-part "@" utf8-domain

utf8-local-part=  utf8-dot-atom / utf8-quoted-string / obs-local-part

utf8-domain    =  utf8-dot-atom / domain-literal / obs-domain

alt-address    =  FWS "<" addr-spec ">"

   Below list a few possible <mailbox> representation as example.


      "DISPLAY_NAME" <ASCII@ASCII>
         ; traditional mailbox format

      "DISPLAY_NAME" <non-ASCII@non-ASCII>
         ; UTF8SMTP but no ALT-ADDRESS parameter provided,
         ; message will bounce if UTF8SMTP extension is not supported

      <non-ASCII@non-ASCII>
             ; without DISPLAY_NAME and quoted string
             ; UTF8SMTP but no ALT-ADDRESS parameter provided,
         ; message will bounce if UTF8SMTP extension is not supported

      "DISPLAY_NAME" <non-ASCII@non-ASCII <ASCII@ASCII>>
         ; UTF8SMTP with ALT-ADDRESS parameter provided,
         ; ALT-ADDRESS can be used if downgrade is necessary
item-value      =/      utf8-addr-spec


