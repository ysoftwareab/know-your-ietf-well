tagURI        = "tag:" taggingEntity ":" specific [ "#" fragment ]

taggingEntity = authorityName "," date
authorityName = DNSname / emailAddress
date          = year ["-" month ["-" day]]
year          = 4DIGIT
month         = 2DIGIT
day           = 2DIGIT
DNSname       = DNScomp *( "." DNScomp )  ; see RFC 1035 [3]
DNScomp       = alphaNum [*(alphaNum /"-") alphaNum]
emailAddress  = 1*(alphaNum /"-"/"."/"_") "@" DNSname
alphaNum      = DIGIT / ALPHA
specific      = *( pchar / "/" / "?" ) ; pchar from RFC 3986  [1]
fragment      = *( pchar / "/" / "?" ) ; same as RFC 3986  [1]

