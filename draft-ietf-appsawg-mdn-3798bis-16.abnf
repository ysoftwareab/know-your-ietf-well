mdn-request-header = "Disposition-Notification-To" ":" mailbox-list CRLF

   A Disposition-Notification-To header field can appear at most once in
   a message.

   The presence of a Disposition-Notification-To header field in a
   message is merely a request for an MDN.  The recipients' user agents
   are always free to silently ignore such a request.

   An MDN MUST NOT itself have a Disposition-Notification-To header
   field.  An MDN MUST NOT be generated in response to an MDN.

   A user agent MUST NOT issue more than one MDN on behalf of each
   particular recipient.  That is, once an MDN has been issued on behalf
   of a recipient, no further MDNs may be issued on behalf of that
   recipient by the same user agent, even if another disposition is
   performed on the message.  However, if a message is forwarded, an MDN
   may have been issued for the recipient doing the forwarding and the
   recipient of the forwarded message may also cause an MDN to be
   generated.

   It is also possible that if the same message is being accessed by
   multiple user agents (for example using POP3), then multiple
   dispositions might be generated for the same recipient.  User agents
   SHOULD leverage support in the underlying message access protocol to
   prevent multiple MDNs from being generated.  In particular, when the
   user agent is accessing the message using RFC-IMAP [RFC3501], it
   SHOULD implement the procedures specified in RFC-IMAP-MDN [RFC3503].
   While Internet standards normally do not specify the behavior of user
   interfaces, it is strongly recommended that the user agent obtain the
   user's consent before sending an MDN.  This consent could be obtained
   for each message through some sort of prompt or dialog box, or
   globally through the user's setting of a preference.  The user might
   also indicate globally that MDNs are to never be sent.  The purpose
   of obtaining user's consent is to protect user's privacy.  The
   default value should be not to send MDNs.

   MDNs MUST NOT be sent automatically if the address in the
   Disposition-Notification-To header field differs from the address in
   the Return-Path header field (see RFC-MSGFMT [RFC5322]).  In this
   case, confirmation from the user MUST be obtained, if possible.  If
   obtaining consent is not possible (e.g., because the user is not
   online at the time or the client is not an interactive email client),
   then an MDN MUST NOT be sent.

   Confirmation from the user MUST be obtained (or no MDN sent) if there
   is no Return-Path header field in the message, or if there is more
   than one distinct address in the Disposition-Notification-To header
   field.

   The comparison of the addresses is done using only the addr-spec
   (local-part "@" domain) portion, excluding any angle brackets, phrase
   and route.  As prescribed by RFC 5322, the comparison is case-
   sensitive for the local-part and case-insensitive for the domain
   part.  The local-part comparison SHOULD be done after performing
   local-part canonicalization (i.e. after removing the surrounding
   double-quote characters, if any, as well as any escaping "\"
   characters.  (See RFC-MSGFMT [RFC5322] for more details.)
   Implementations MAY treat known domain aliases as equivalent for the
   purpose of comparison.

   Note that use of subaddressing (see [RFC5233]) can result in a
   failure to match two local-parts and thus result in possible
   suppression of the MDN.  This document doesn't recommend special
   handling for this case, as the receiving MUA can't reliably know
   whether or not the sender is using subaddressing.

   If the message contains more than one Return-Path header field, the
   implementation may pick one to use for the comparison, or treat the
   situation as a failure of the comparison.

   The reason for not automatically sending an MDN if the comparison
   fails or more than one address is specified is to reduce the
   possibility of mail loops and of MDNs being used for mail bombing.
   It's especially important that a message that contains a Disposition-
   Notification-To header field also contain a Message-ID header field,
   to permit user agents to automatically correlate MDNs with their
   original messages.

   If the request for message disposition notifications for some
   recipients and not others is desired, two copies of the message
   should be sent, one with a Disposition-Notification-To header field
   and one without.  Many of the other header fields of the message
   (e.g., To, Cc) will be the same in both copies.  The recipients in
   the respective message envelopes determine from whom message
   disposition notifications are requested and from whom they are not.
   If desired, the Message-ID header field may be the same in both
   copies of the message.  Note that there are other situations (e.g.,
   Bcc) in which it is necessary to send multiple copies of a message
   with slightly different header fields.  The combination of such
   situations and the need to request MDNs for a subset of all
   recipients may result in more than two copies of a message being
   sent, some with a Disposition-Notification-To header field and some
   without.

   If it is possible to determine that a recipient is a newsgroup, do
   not include a Disposition-Notification-To header field for that
   recipient.  Similarly, if an existing message is resent or gatewayed
   to a newsgroup, the agent doing resending/gatewaying SHOULD strip the
   Disposition-Notification-To header field.  See Section 5 for more
   discussion.  Clients that see an otherwise valid Disposition-
   Notification-To header field in a newsgroup message SHOULD NOT
   generate an MDN.

Disposition-Notification-Options =
          "Disposition-Notification-Options" ":" [FWS]
                         disposition-notification-parameter-list CRLF

disposition-notification-parameter-list =
          disposition-notification-parameter
          *([FWS] ";" [FWS] disposition-notification-parameter)

disposition-notification-parameter = attribute [FWS] "="
          [FWS] importance [FWS] "," [FWS] value *([FWS] "," [FWS] value)

importance = "required" / "optional"

attribute = atom

value = word

   A Disposition-Notification-Options header field can appear at most
   once in a message.

   An importance of "required" indicates that interpretation of the
   disposition-notification-parameter is necessary for proper generation
   of an MDN in response to this request.  An importance of "optional"
   indicates that an MUA that does not understand the meaning of this
   disposition-notification-parameter MAY generate an MDN in response
   anyway, ignoring the value of the disposition-notification-parameter.

   No disposition-notification-parameter attribute names are defined in
   this specification.  Attribute names may be defined in the future by
   later revisions or extensions to this specification.  disposition-
   notification-parameter attribute names MUST be registered with the
   Internet Assigned Numbers Authority (IANA) using "Specification
   required" registration policy.  The "X-" prefix has historically been
   used to denote unregistered "experimental" protocol elements, that
   are assumed not to become common use.  Deployment experience of this
   and other protocols have shown that this assumption is often false.
   This document allows the use of the "X-" prefix primarily to allow
   the registration of attributes that are already in common use.  The
   prefix has no meaning for new attributes.  Its use in substantially
   new attributes may cause confusion and is therefore discouraged.
   (See Section 10 for a registration form.)

original-recipient-header =
          "Original-Recipient" ":" OWS address-type OWS ";" OWS generic-address OWS

OWS      = [CFWS]
           ; Optional whitespace.
           ; MDN generators SHOULD use "*WSP"
           ; (typically a single space or nothing.
           ; It SHOULD be nothing at the end of a field),
           ; unless an RFC 5322 "comment" is required.
           ;
           ; MDN parsers MUST parse it as "[CFWS]".

   The address-type and generic-address token are as specified in the
   description of the Original-Recipient field in Section 3.2.3.

   The purpose of carrying the original recipient information and
   returning it in the MDN is to permit automatic correlation of MDNs
   with the original message on a per-recipient basis.

disposition-notification-content = [ reporting-ua-field CRLF ]
             [ mdn-gateway-field CRLF ]
             [ original-recipient-field CRLF ]
             final-recipient-field CRLF
             [ original-message-id-field CRLF ]
             disposition-field CRLF
             *( error-field CRLF )
             *( extension-field CRLF )

extension-field = extension-field-name ":" *([FWS] text)

extension-field-name = field-name

address-type = atom

atom = <The version from RFC 5321 (not from RFC 5322) is used in this document.>


   b.  An "MTA-name-type" specifies the format of a mail transfer agent
       name.  For example, for an SMTP server on an Internet host, the
       MTA name is the domain name of that host, and the "dns" MTA-name-
       type is used.  Other values can appear in this field as specified
       in the "MTA Name Types" IANA subregistry established by RFC-DSN-
       FORMAT [RFC3464].

mta-name-type = atom

reporting-ua-field = "Reporting-UA" ":" OWS ua-name OWS [ ";" OWS ua-product OWS ]

ua-name = *text-no-semi

ua-product = *([FWS] text)

text-no-semi = %d1-9 /         ; "text" characters excluding NUL, CR,
               %d11 / %d12 / %d14-58 / %d60-127  ; LF, or semi-colon

   The Reporting-UA field is defined as follows:
   An MDN describes the disposition of a message after it has been
   delivered to a recipient.  In all cases, the Reporting-UA is the MUA
   that performed the disposition described in the MDN.

   The "Reporting-UA" field contains information about the MUA that
   generated the MDN, which is often used by servers to help identify
   the scope of reported interoperability problems, to work around or
   tailor responses to avoid particular MUA limitations, and for
   analytics regarding MUA or operating system use.  A MUA SHOULD send a
   "Reporting-UA" field unless specifically configured not to do so.

   If the reporting MUA consists of more than one component (e.g., a
   base program and plug-ins), this may be indicated by including a list
   of product names.

   A reporting MUA SHOULD limit generated product identifiers to what is
   necessary to identify the product; a sender MUST NOT generate
   advertising or other nonessential information within the product
   identifier.

   A reporting MUA SHOULD NOT generate a "Reporting-UA" field containing
   needlessly fine-grained detail and SHOULD limit the addition of
   subproducts by third parties.  Overly long and detailed "Reporting-
   UA" field values increase the risk of a user being identified against
   their wishes ("fingerprinting").

   Likewise, implementations are encouraged not to use the product
   tokens of other implementations in order to declare compatibility
   with them, as this circumvents the purpose of the field.  If a MUA
   masquerades as a different MUA, recipients can assume that the user
   intentionally desires to see responses tailored for that identified
   MUA, even if they might not work as well for the actual MUA being
   used.

   Example:

   Reporting-UA:  Foomail 97.1

mdn-gateway-field = "MDN-Gateway" ":" OWS mta-name-type OWS ";" OWS mta-name OWS

mta-name = *text

   For gateways into Internet Mail, the MTA-name-type will normally be
   "dns", and the mta-name will be the Internet domain name of the
   gateway.

original-recipient-field =
          "Original-Recipient" ":" OWS address-type OWS ";" OWS generic-address OWS

generic-address = *text

   The address-type field indicates the type of the original recipient
   address.  If the message originated within the Internet, the address-
   type field will normally be "rfc822", and the address will be
   according to the syntax specified in RFC-MSGFMT [RFC5322].  The value
   "unknown" should be used if the Reporting MUA cannot determine the
   type of the original recipient address from the message envelope.
   This address is the same as that provided by the sender and can be
   used to automatically correlate MDN reports with original messages on
   a per recipient basis.

final-recipient-field =
          "Final-Recipient" ":" OWS address-type OWS ";" OWS generic-address OWS
   The generic-address subfield of the Final-Recipient field SHOULD
   contain the mailbox address of the recipient (which will be the same
   as the From header field of the MDN) as it was when the MDN was
   generated by the MUA.

      One example of when this field might not contain the final
      recipient address of the message is when an alias (e.g. "customer-
      support@example.com") forwards mail to a specific personal address
      (e.g. "bob@example.com").  Bob might want to be able to send MDNs,
      but not give away his personal email address.  In this case the
      Final-Recipient field can be "customer-support@example.com"
      instead of "bob@example.com".

   The Final-Recipient address may differ from the address originally
   provided by the sender, because it may have been transformed during
   forwarding and gatewaying into a totally unrecognizable mess.
   However, in the absence of the optional Original-Recipient field, the
   Final-Recipient field and any returned content may be the only
   information available with which to correlate the MDN with a
   particular message recipient.

   The address-type subfield indicates the type of address expected by
   the reporting MTA in that context.  Recipient addresses obtained via
   SMTP will normally be of address-type "rfc822", but can be other
   values from the "Address Types" subregistry of the "Delivery Status
   Notification (DSN) Types" IANA registry.

   Since mailbox addresses (including those used in the Internet) may be
   case sensitive, the case of alphabetic characters in the address MUST
   be preserved.

original-message-id-field =
             "Original-Message-ID" ":" msg-id

disposition-field =
             "Disposition" ":" OWS disposition-mode OWS ";"
             OWS disposition-type
             [ OWS "/" OWS disposition-modifier
             *( OWS "," OWS disposition-modifier ) ] OWS

disposition-mode = action-mode OWS "/" OWS sending-mode

action-mode = "manual-action" / "automatic-action"

sending-mode = "MDN-sent-manually" / "MDN-sent-automatically"

disposition-type = "displayed" / "deleted" / "dispatched" /
             "processed"

disposition-modifier = "error" / disposition-modifier-extension

disposition-modifier-extension = atom

error-field = "Error" ":" *([FWS] text)

OWS          = [CFWS]
                  ; Optional whitespace.
                  ; MDN generators SHOULD use "*WSP"
                  ; (typically a single space or nothing.
                  ; It SHOULD be nothing at the end of a field),
                  ; unless an RFC 5322 "comment" is required.
                  ;
                  ; MDN parsers MUST parse it as "[CFWS]".

mdn-request-header =
          "Disposition-Notification-To" ":" mailbox-list CRLF

Disposition-Notification-Options =
          "Disposition-Notification-Options" ":" [FWS]
                    disposition-notification-parameter-list CRLF

disposition-notification-parameter-list =
                    disposition-notification-parameter
                    *([FWS] ";" [FWS] disposition-notification-parameter)

disposition-notification-parameter = attribute [FWS] "=" [FWS]
                    importance [FWS] "," [FWS] value *([FWS] "," [FWS] value)

importance = "required" / "optional"

attribute = atom
value = word

original-recipient-header =
          "Original-Recipient" ":" OWS address-type OWS ";" OWS generic-address OWS CRLF

disposition-notification-content =
          [ reporting-ua-field CRLF ]
          [ mdn-gateway-field CRLF ]
          [ original-recipient-field CRLF ]
          final-recipient-field CRLF
          [ original-message-id-field CRLF ]
          disposition-field CRLF
          *( error-field CRLF )
          *( extension-field CRLF )

address-type = atom

mta-name-type = atom

reporting-ua-field = "Reporting-UA" ":" OWS ua-name OWS [ ";" OWS ua-product OWS ]

ua-name = *text-no-semi

ua-product = *([FWS] text)

text-no-semi = %d1-9 /        ; "text" characters excluding NUL, CR,
           %d11 / %d12 / %d14-58 / %d60-127      ; LF, or semi-colon

mdn-gateway-field = "MDN-Gateway" ":" OWS mta-name-type OWS ";" OWS mta-name

mta-name = *text

original-recipient-field =
          "Original-Recipient" ":" OWS address-type OWS ";" OWS generic-address OWS

generic-address = *text

final-recipient-field =
          "Final-Recipient" ":" OWS address-type OWS ";" OWS generic-address OWS

original-message-id-field = "Original-Message-ID" ":" msg-id

disposition-field =
          "Disposition" ":" OWS disposition-mode OWS ";"
          OWS disposition-type
          [ OWS "/" OWS disposition-modifier
          *( OWS "," OWS disposition-modifier ) ] OWS
disposition-mode = action-mode OWS "/" OWS sending-mode

action-mode = "manual-action" / "automatic-action"

sending-mode = "MDN-sent-manually" / "MDN-sent-automatically"

disposition-type = "displayed" / "deleted" / "dispatched" /
           "processed"

disposition-modifier = "error" / disposition-modifier-extension

disposition-modifier-extension = atom

error-field = "Error" ":" *([FWS] text)

extension-field = extension-field-name ":" *([FWS] text)

extension-field-name = field-name

boundary="RAA14128.773615765/example.com"

