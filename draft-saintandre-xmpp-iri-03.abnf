xmppiri    = "xmpp" ":" ihierxmpp
                  [ "?" iquerycomp ]
                  [ "#" ifragment ]
ihierxmpp  = iauthpath / ipathxmpp
iauthpath  = "//" iauthxmpp [ "/" ipathxmpp ]
iauthxmpp  = inodeid "@" ihost
ipathxmpp  = [ inodeid "@" ] ihost [ "/" iresid ]
inodeid    = *( iunreserved / pct-encoded / nodeallow )
nodeallow  = "!" / "$" / "(" / ")" / "*" / "+" / "," / ";" /
                  "=" / "[" / "\" / "]" / "^" / "`" / "{" / "|" /
                  "}"
iresid     = *( iunreserved / pct-encoded / resallow )
resallow   = "!" / '"' / "$" / "'" / "(" / ")" / "*" / "+" /
                  "," / ":" / ";" / "<" / "=" / ">" / "[" / "\" /
                  "]" / "^" / "`" / "{" / "|" / "}"
iquerycomp = iquerytype [ *ipair ]
iquerytype = *iunreserved
ipair      = ";" ikey "=" ivalue
ikey       = *iunreserved
ivalue     = *( iunreserved / pct-encoded )

xmppuri   = "xmpp" ":" hierxmpp [ "?" querycomp ] [ "#" fragment ]
hierxmpp  = authpath / pathxmpp
authpath  = "//" authxmpp [ "/" pathxmpp ]
authxmpp  = nodeid "@" host
pathxmpp  = [ nodeid "@" ] host [ "/" resid ]
nodeid    = *( unreserved / pct-encoded / nodeallow )
nodeallow = "!" / "$" / "(" / ")" / "*" / "+" / "," / ";" /
                 "=" / "[" / "\" / "]" / "^" / "`" / "{" / "|" /
                 "}"
resid     = *( unreserved / pct-encoded / resallow )
resallow  = "!" / '"' / "$" / "'" / "(" / ")" / "*" / "+" /
                 "," / ":" / ";" / "<" / "=" / ">" / "[" / "\" /
                 "]" / "^" / "`" / "{" / "|" / "}"
querycomp = querytype [ *pair ]
querytype = *( unreserved / pct-encoded )
pair      = ";" key "=" value
key       = *( unreserved / pct-encoded )
value     = *( unreserved / pct-encoded )


