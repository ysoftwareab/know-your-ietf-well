authres-header = "Authentication-Results:" [CFWS] authserv-id
              [ CFWS version ]
              ( [CFWS] ";" [CFWS] "none" / 1*resinfo ) [CFWS] CRLF
            ; the special case of "none" is used to indicate that no
            ; message authentication is performed


authserv-id = dot-atom-text
                 ; see below for a description of this element;
                 ; "dot-atom-text" is defined in section 3.2.3 of [MAIL]


version = 1*DIGIT [CFWS]
             ; indicates which version of this specification is in use;
             ; this specification is version "1"; the absence of a
             ; version implies this version of the specification


resinfo = [CFWS] ";" methodspec [ CFWS reasonspec ]
               *( CFWS propspec )


methodspec = [CFWS] method [CFWS] "=" [CFWS] result
                ; indicates which authentication method was evaluated


reasonspec = "reason" [CFWS] "=" [CFWS] value
                ; a free-form comment on the reason the given result
                ; was returned


propspec = ptype [CFWS] "." [CFWS] property [CFWS] "=" pvalue
              ; an indication of which properties of the message
              ; were evaluated by the authentication scheme being
              ; applied to yield the reported result and would be
              ; useful to reveal to end users as authenticated


method = token [ [CFWS] "/" [CFWS] version ]
            ; a method indicates which method's result is
            ; represented by "result", and is one of the methods
            ; explicitly defined as valid in this document
            ; or is an extension method as defined below


result = token
            ; indicates the results of the attempt to authenticate
            ; the message; see below for details


ptype = "smtp" / "header" / "body" / "policy"
           ; indicates whether the property being evaluated was
           ; a parameter to an [SMTP] command, or was a value taken
           ; from a message header field, or was some property of
           ; the message body, or some other property evaluated by
           ; the receiving MTA
property = token
             ; if "ptype" is "smtp", this indicates which [SMTP]
             ; command provided the value which was evaluated by the
             ; authentication scheme being applied; if "ptype" is
             ; "header", this indicates from which header field the
             ; value being evaluated was extracted; if "ptype" is
             ; "body", this indicates the offset into the body at which
             ; content of interest was detected; if "ptype" is "policy"
             ; then this indicates the name of the policy which caused
             ; this header field to be added (see below)


pvalue = [CFWS] ( token / addr-spec ) [CFWS]
            ; the value extracted from the message property defined
            ; by the "ptype.property" construction; if the value
            ; identifies an address, then it is an "addr-spec"

foo=pass bar.baz=blob (2 of 3 tests OK)

spf=pass smtp.mailfrom=example.net
auth=pass (cram-md5) smtp.auth=sender@example.com;
spf=pass smtp.mailfrom=example.com
sender-id=pass header.from=example.com
sender-id=hardfail header.from=example.com;
dkim=pass (good signature) header.i=sender@example.com
auth=pass (cram-md5) smtp.auth=sender@example.com;
spf=hardfail smtp.mailfrom=example.com
t=1188964191; c=simple/simple;
h=From:Date:To:Message-Id:Subject;
bh=sEuZGD/pSr7ANysbY3jtdaQ3Xv9xPQtS0m70;
b=EToRSuvUfQVP3Bkz ... rTB0t0gYnBVCM=
dkim=pass (good signature) header.i=@mail-router.example.net;
dkim=fail (bad signature) header.i=@newyork.example.com
d=mail-router.example.net; t=1188964198; c=relaxed/simple;
h=From:Date:To:Message-Id:Subject:Authentication-Results;
bh=ftA9J6GtX8OpwUECzHnCkRzKw1uk6FNiLfJl5Nmv49E=;
b=oINEO8hgn/gnunsg ... 9n9ODSNFSDij3=
dkim=pass (good signature) header.i=@newyork.example.com
t=1188964191; c=simple/simple;
h=From:Date:To:Message-Id:Subject;
bh=sEu28nfs9fuZGD/pSr7ANysbY3jtdaQ3Xv9xPQtS0m7=;
b=EToRSuvUfQVP3Bkz ... rTB0t0gYnBVCM=
