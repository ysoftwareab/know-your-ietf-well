snmpStatement           = snmpKeyword *1(sep lcIdentifier) optsep
                                 \"{\" stmtsep
                                 *1(oidStatement stmtsep)
                                 *(nodeStatement stmtsep)
                                 *(scalarsStatement stmtsep)
                                 *(tableStatement stmtsep)
                                 *(notificationStatement stmtsep)
                                 *(groupStatement stmtsep)
                                 *(complianceStatement stmtsep)
                                 statusStatement stmtsep
                                 descriptionStatement stmtsep
                                 *1(referenceStatement stmtsep)
                             \"}\" optsep \";\"

nodeStatement           = nodeKeyword sep lcIdentifier optsep
                                 \"{\" stmtsep
                                 oidStatement stmtsep
                                 *1(representsStatement stmtsep)
                                 statusStatement stmtsep
                                 *1(descriptionStatement stmtsep)
                                 *1(referenceStatement stmtsep)
                             \"}\" optsep \";\"

representsStatement     = representsKeyword sep
                                 qucIdentifier optsep \";\"

scalarsStatement        = scalarsKeyword sep lcIdentifier optsep
                                 \"{\" stmtsep
                                 oidStatement stmtsep
                                 1*(objectStatement stmtsep)
                                 statusStatement stmtsep
                                 descriptionStatement stmtsep
                                 *1(referenceStatement stmtsep)
                             \"}\" optsep \";\"

tableStatement          = tableKeyword sep lcIdentifier optsep
                                 \"{\" stmtsep
                                 oidStatement stmtsep
                                 anyIndexStatement stmtsep
                                 *1(createStatement stmtsep)
                                 1*(objectStatement stmtsep)
                                 statusStatement stmtsep
                                 descriptionStatement stmtsep
                                 *1(referenceStatement stmtsep)
                             \"}\" optsep \";\"

objectStatement         = objectKeyword sep lcIdentifier optsep
                                 \"{\" stmtsep
                                 implementsStatement stmtsep
                                 *1(subidStatement stmtsep)
                                 *1(statusStatement stmtsep)
                                 *1(descriptionStatement stmtsep)
                                 *1(referenceStatement stmtsep)
                             \"}\" optsep \";\"

implementsStatement     = implementsKeyword sep qcattrIdentifier
                                 optsep \";\"

notificationStatement   = notificationKeyword sep lcIdentifier
                                 optsep \"{\" stmtsep
                                 oidStatement stmtsep
                                 signalsStatement stmtsep
                                 statusStatement stmtsep
                                 descriptionStatement stmtsep
                                 *1(referenceStatement stmtsep)
                             \"}\" optsep \";\"

signalsStatement        = signalsKeyword sep qattrIdentifier
                                 optsep \"{\" stmtsep
                                 *(signalsObjectStatement)
                             \"}\" optsep \";\"

signalsObjectStatement  = objectKeyword sep
                                 qattrIdentifier optsep \";\"

groupStatement          = groupKeyword sep lcIdentifier optsep
                                 \"{\" stmtsep
                                 oidStatement stmtsep
                                 membersStatement stmtsep
                                 statusStatement stmtsep
                                 descriptionStatement stmtsep
                                 *1(referenceStatement stmtsep)
                             \"}\" optsep \";\"

complianceStatement     = complianceKeyword sep lcIdentifier optsep
                                 \"{\" stmtsep
                                 oidStatement stmtsep
                                 statusStatement stmtsep
                                 descriptionStatement stmtsep
                                 *1(referenceStatement stmtsep)
                                 *1(mandatoryStatement stmtsep)
                                 *(optionalStatement stmtsep)
                                 *(refineStatement stmtsep)
                             \"}\" optsep \";\"

anyIndexStatement       = indexStatement /
                             augmentsStatement /
                             reordersStatement /
                             extendsStatement /
                             expandsStatement

indexStatement          = indexKeyword *1(sep impliedKeyword) optsep
                                 \"(\" optsep qlcIdentifierList
                                 optsep \")\" optsep \";\"

augmentsStatement       = augmentsKeyword sep qlcIdentifier
                                 optsep \";\"

reordersStatement       = reordersKeyword sep qlcIdentifier
                                 *1(sep impliedKeyword)
                                 optsep \"(\" optsep
                                 qlcIdentifierList optsep \")\"
                                 optsep \";\"

extendsStatement        = extendsKeyword sep qlcIdentifier optsep \";\"

expandsStatement        = expandsKeyword sep qlcIdentifier
                                 *1(sep impliedKeyword)
                                 optsep \"(\" optsep
                                 qlcIdentifierList optsep \")\"
                                 optsep \";\"

createStatement         = createKeyword optsep \";\"

membersStatement        = membersKeyword optsep \"(\" optsep
                                 qlcIdentifierList optsep
                                 \")\" optsep \";\"

mandatoryStatement      = mandatoryKeyword optsep \"(\" optsep
                                 qlcIdentifierList optsep
                                 \")\" optsep \";\"

optionalStatement       = optionalKeyword sep qlcIdentifier optsep
                                 \"{\" descriptionStatement stmtsep
                             \"}\" optsep \";\"

refineStatement         = refineKeyword sep qlcIdentifier optsep \"{\"
                                 *1(typeStatement stmtsep)
                                 *1(writetypeStatement stmtsep)
                                 *1(accessStatement stmtsep)
                                 descriptionStatement stmtsep
                             \"}\" optsep \";\"

typeStatement           = typeKeyword sep
                                 (refinedBaseType / refinedType)
                                 optsep \";\"

writetypeStatement      = writetypeKeyword sep
                                 (refinedBaseType / refinedType)
                                 optsep \";\"

oidStatement            = oidKeyword sep objectIdentifier optsep \";\"

subidStatement          = subidKeyword sep subid optsep \";\"

snmpKeyword         =  %x73 %x6E %x6D %x70
nodeKeyword         =  %x6E %x6F %x64 %x65
representsKeyword   =  %x72 %x65 %x70 %x72 %x65 %x73 %x65 %x6E %x74
                          %x73
scalarsKeyword      =  %x73 %x63 %x61 %x6C %x61 %x72 %x73
tableKeyword        =  %x74 %x61 %x62 %x6C %x65
implementsKeyword   =  %x69 %x6D %x70 %x6C %x65 %x6D %x65 %x6E %x74
                          %x73
subidKeyword        =  %x73 %x75 %x62 %x69 %x64
objectKeyword       =  %x6F %x62 %x6A %x65 %x63 %x74
notificationKeyword =  %x6E %x6F %x74 %x69 %x66 %x69 %x63 %x61 %x74
                          %x69 %x6F %x6E
signalsKeyword      =  %x73 %x69 %x67 %x6E %x61 %x6C %x73
oidKeyword          =  %x6F %x69 %x64
groupKeyword        =  %x67 %x72 %x6F %x75 %x70
complianceKeyword   =  %x63 %x6F %x6D %x70 %x6C %x69 %x61 %x6E %x63
                          %x65
impliedKeyword      =  %x69 %x6D %x70 %x6C %x69 %x65 %x64
indexKeyword        =  %x69 %x6E %x64 %x65 %x78
augmentsKeyword     =  %x61 %x75 %x67 %x6D %x65 %x6E %x74 %x73
reordersKeyword     =  %x72 %x65 %x6F %x72 %x64 %x65 %x72 %x73
extendsKeyword      =  %x65 %x78 %x74 %x65 %x6E %x64 %x73
expandsKeyword      =  %x65 %x78 %x70 %x61 %x6E %x64 %x73
createKeyword       =  %x63 %x72 %x65 %x61 %x74 %x65
membersKeyword      =  %x6D %x65 %x6D %x62 %x65 %x72 %x73
mandatoryKeyword    =  %x6D %x61 %x6E %x64 %x61 %x74 %x6F %x72 %x79
optionalKeyword     =  %x6F %x70 %x74 %x69 %x6F %x6E %x61 %x6C
refineKeyword       =  %x72 %x65 %x66 %x69 %x6E %x65
writetypeKeyword    =  %x77 %x72 %x69 %x74 %x65 %x74 %x79 %x70 %x65

