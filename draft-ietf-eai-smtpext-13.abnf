uMailbox = uLocal-part "@" uDomain
           ; Replace Mailbox in RFC 2821, section 4.1.2


uLocal-part = uDot-string / uQuoted-string
           ; MAY be case-sensitive
           ; Replace Local-part in RFC 2821, section 4.1.2


uDot-string = uAtom *("." uAtom)
           ; Replace Dot-string in RFC 2821, section 4.1.2


uAtom = 1*ucharacter
               ; Replace Atom in RFC 2821, section 4.1.2


ucharacter = atext / UTF8-non-ascii


atext = <See section 3.2.4 of RFC 2822>


uQuoted-string = DQUOTE *uqcontent DQUOTE
           ; Replace Quoted-string in RFC 2821, section 4.1.2


DQUOTE = <See appendix B.1 of RFC 5234>





uqcontent = qcontent / UTF8-non-ascii


qcontent = <See section 3.2.5 of RFC 2822>


uDomain = (sub-udomain 1*("." sub-udomain)) / address-literal
           ; Replace Domain in RFC 2821, section 4.1.2


address-literal = <See section 4.1.2 of RFC 2822>


sub-udomain = uLet-dig [uLdh-str]
           ; Replace sub-domain in RFC 2821, section 4.1.2


uLet-dig = Let-dig / UTF8-non-ascii


Let-dig = <See section 4.1.3 of RFC 2821>


uLdh-str = *( ALPHA / DIGIT / "-" / UTF8-non-ascii) uLet-dig
           ; Replace Ldh-str in RFC 2821, section 4.1.3


UTF8-non-ascii = UTF8-2 / UTF8-3 / UTF8-4


UTF8-2 =  <See section 4 of RFC 3629>


UTF8-3 =  <See section 4 of RFC 3629>


UTF8-4 =  <See section 4 of RFC 3629>

uReverse-path = uPath
           ; Replace Reverse-path in RFC 2821, section 4.1.2


uForward-path = uPath
           ; Replace Forward-path in RFC 2821, section 4.1.2


uPath = "<" [ A-d-l ":" ] uMailbox ">"
           ; Replace Path in RFC 2821, section 4.1.2
           ; uMailbox is defined in section 3.3 of this document


A-d-l = <See section 4.1.2 of RFC 2821>






ALT-ADDRESS-parameter="ALT-ADDRESS=" ALT-ADDRESS-value


ALT-ADDRESS-value= xtext
               ; The value is a mailbox name encoded as xtext.


xtext= <See section 4.2 of RFC 3461>

uReturn-path-line = "Return-Path:" FWS uReverse-path <CRLF>
       ; Replaces Return-path-line in section 4.4 of RFC2821
       ; uReverse-path is defined in Section 3.3 of this document


uTime-stamp-line = "Received:" FWS uStamp <CRLF>
       ; Replaces Time-stamp-line in section 4.4 of RFC2821


uStamp = From-domain By-domain uOpt-info ";"  FWS date-time
       ; Replaces Stamp in section 4.4 of RFC2821


uOpt-info = [Via] [With] [ID] [uFor]
       ; Replaces Opt-info in section 4.4 of RFC2821
       ; The protocol value for With will allow a UTF8SMTP value









uFor = "FOR" ( FWS (uPath / uMailbox) ) CFWS
       ; Replaces For in section 4.4 of RFC2821
       ; uPath and uMailbox are defined in Sections 2.4 and
       ; 2.3, respectively, of this document

