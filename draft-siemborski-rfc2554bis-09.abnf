dT4=
AUTH=mailbox

hexchar         = "+" HEXDIG HEXDIG
xchar           = %x21-2A / %x2C-3C / %x3E-7E
                          ;; US-ASCII except for "+", "=", SP and CTL

xtext           = *(xchar / hexchar)
                          ;; non-US-ASCII is only allowed as hexchar

auth-command    = "AUTH" SP sasl-mech [SP initial-response]
                          *(CRLF [base64]) [CRLF cancel-response]
                          CRLF
                          ;; <sasl-mech> is defined in [SASL]

auth-param      = "AUTH=" xtext
                            ;; Parameter to the MAIL FROM command.
                            ;; This non-terminal complies with
                            ;; syntax defined by esmtp-param [SMTP].
                            ;;
                            ;; The decoded form of the xtext MUST be
                            ;; either a <mailbox> or the two
                            ;; characters "<>"

base64          = base64-terminal /
                          ( 1*(4base64-char) [base64-terminal] )

base64-char     = ALPHA / DIGIT / "+" / "/"
                          ;; Case-sensitive

base64-terminal = (2base64-char "==") / (3base64-char "=")

continue-req    = "334" SP [base64] CRLF
                            ;; Intermediate response to the AUTH
                            ;; command.
                            ;; This non-terminal complies with
                            ;; syntax defined by Reply-line [SMTP].

initial-response= base64 / "="

cancel-response = "*"



