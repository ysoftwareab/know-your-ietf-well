PrintableCharacter = ALPHA / DIGIT / SQUOTE / LPAREN / RPAREN /
                           PLUS / COMMA / HYPHEN / DOT / EQUALS /
                           SLASH / COLON / QUESTION / SPACE
PrintableString    = 1*PrintableCharacter
IA5String          = *(%x00-7F)
SLASH              = %x2F  ; forward slash ("/")
COLON              = %x3A  ; colon (":")
QUESTION           = %x3F  ; question mark ("?")

BitString    = SQUOTE *binary-digit SQUOTE "B"
binary-digit = "0" / "1"

Boolean = "TRUE" / "FALSE"

CountryString  = 2(PrintableCharacter)

DeliveryMethod = pdm *( WSP DOLLAR WSP pdm )

pdm = "any" / "mhs" / "physical" / "telex" / "teletex" /
            "g3fax" / "g4fax" / "ia5" / "videotex" / "telephone"

DirectoryString = 1*UTF8

UID=jsmith,DC=example,DC=net
OU=Sales+CN=J. Smith,DC=example,DC=net
CN=John Smith\, III,DC=example,DC=net
CN=Before\0dAfter,DC=example,DC=net
CN=Lu\C4\8Di\C4\87

EnhancedGuide = object-class SHARP WSP criteria WSP
                         SHARP WSP subset
object-class  = WSP oid WSP
subset        = "baseobject" / "oneLevel" / "wholeSubtree"

criteria   = and-term *( BAR and-term )
and-term   = term *( AMPERSAND term )
term       = EXCLAIM term /
                   attributetype DOLLAR match-type /
                   LPAREN criteria RPAREN /
                   true /
                   false
match-type = "EQ" / "SUBSTR" / "GE" / "LE" / "APPROX"
true       = "?true"
false      = "?false"
BAR        = %x7C  ; vertical bar ("|")
AMPERSAND  = %x26  ; ampersand ("&")
EXCLAIM    = %x21  ; exclamation mark ("!")

fax-number       = telephone-number *( DOLLAR fax-parameter )
telephone-number = PrintableString
fax-parameter    = "twoDimensional" /
                         "fineResolution" /
                         "unlimitedLength" /
                         "b4Length" /
                         "a3Width" /
                         "b4Width" /
                         "uncompressed"

GeneralizedTime = century year month day hour
                           [ minute [ second / leap-second ] ]
                           [ fraction ]
                           g-time-zone

century = 2(%x30-39) ; "00" to "99"
year    = 2(%x30-39) ; "00" to "99"
month   =   ( %x30 %x31-39 ) ; "01" (January) to "09"
                / ( %x31 %x30-32 ) ; "10" to "12"
day     =   ( %x30 %x31-39 )    ; "01" to "09"
                / ( %x31-32 %x30-39 ) ; "10" to "29"
                / ( %x33 %x30-31 )    ; "30" to "31"
hour    = ( %x30-31 %x30-39 ) / ( %x32 %x30-33 ) ; "00" to "23"
minute  = %x30-35 %x30-39                        ; "00" to "59"

second      = ( %x30-35 %x30-39 ) ; "00" to "59"
leap-second = ( %x36 %x30 )       ; "60"

fraction        = ( DOT / COMMA ) 1*(%x30-39)
g-time-zone     = %x5A  ; "Z"
                        / g-differential
g-differential  = ( MINUS / PLUS ) hour [ minute ]
MINUS           = %x2D  ; minus sign ("-")

Guide = [ object-class SHARP ] criteria

Integer = ( HYPHEN LDIGIT *DIGIT ) / number

NameAndOptionalUID = distinguishedName [ SHARP BitString ]

NumericString = 1*(DIGIT / SPACE)

OctetString = *OCTET

OtherMailbox = mailbox-type DOLLAR mailbox
mailbox-type = PrintableString
mailbox      = IA5String

PostalAddress = line *( DOLLAR line )
line          = 1*line-char
line-char     = %x00-23
                      / (%x5C "24")  ; escaped "$"
                      / %x25-5B
                      / (%x5C "5C")  ; escaped "\"
                      / %x5D-7F
                      / UTFMB

SubstringAssertion = [ initial ] any [ final ]

initial  = substring
any      = ASTERISK *(substring ASTERISK)
final    = substring
ASTERISK = %x2A  ; asterisk ("*")

substring           = 1*substring-character
substring-character = %x00-29
                            / (%x5C "2A")  ; escaped "*"
                            / %x2B-5B
                            / (%x5C "5C")  ; escaped "\"
                            / %x5D-7F
                            / UTFMB

teletex-id = ttx-term *(DOLLAR ttx-param)
ttx-term   = PrintableString          ; terminal identifier
ttx-param  = ttx-key COLON ttx-value  ; parameter
ttx-key    = "graphic" / "control" / "misc" / "page" / "private"
ttx-value  = *ttx-value-octet

ttx-value-octet = %x00-23
                        / (%x5C "24")  ; escaped "$"
                        / %x25-5B
                        / (%x5C "5C")  ; escaped "\"
                        / %x5D-FF

telex-number  = actual-number DOLLAR country-code
                         DOLLAR answerback
actual-number = PrintableString
country-code  = PrintableString
answerback    = PrintableString

UTCTime         = year month day hour minute [ second ]
                           [ u-time-zone ]
u-time-zone     = %x5A  ; "Z"
                        / u-differential
u-differential  = ( MINUS / PLUS ) hour minute

