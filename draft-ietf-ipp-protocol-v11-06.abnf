ipp-message = ipp-request / ipp-response
ipp-request = version-number operation-id request-id
           *attribute-group end-of-attributes-tag data
ipp-response = version-number status-code request-id
           *attribute-group end-of-attributes-tag  data
attribute-group = begin-attribute-group-tag attribute



version-number = major-version-number minor-version-number
major-version-number = SIGNED-BYTE
minor-version-number = SIGNED-BYTE
operation-id = SIGNED-SHORT    ; mapping from model defined below
status-code = SIGNED-SHORT  ; mapping from model defined below
request-id = SIGNED-INTEGER ; whose value is > 0

attribute = attribute-with-one-value *additional-value

attribute-with-one-value = value-tag name-length name
      value-length value
additional-value = value-tag zero-name-length value-length value

name-length = SIGNED-SHORT    ; number of octets of 'name'
name = LALPHA *( LALPHA / DIGIT / "-" / "_" / "." )
value-length = SIGNED-SHORT   ; number of octets of 'value'
value = OCTET-STRING

data = OCTET-STRING

zero-name-length = %x00.00            ; name-length of 0
value-tag = %x10-FF                  ;see section 3.7.2
begin-attribute-group-tag = %x00-02 / %04-0F ; see section 3.7.1
end-of-attributes-tag = %x03                  ; tag of 3
                                ; see section 3.7.1
SIGNED-BYTE = BYTE
SIGNED-SHORT = 2BYTE
SIGNED-INTEGER = 4BYTE
DIGIT = %x30-39    ;  "0" to "9"
LALPHA = %x61-7A   ;  "a" to "z"
BYTE = %x00-FF
OCTET-STRING = *BYTE

delimiter-tag = begin-attribute-group-tag  / ; see section 3.7.1
            end-of-attributes-tag 
delimiter-tag = %x00-0F                      ; see section 3.7.1

begin-attribute-group-tag = %x00 / operation-attributes-tag /
     job-attributes-tag / printer-attributes-tag /
     unsupported-attributes-tag /  %x06-0F
operation-attributes-tag =  %x01              ; tag of 1
job-attributes-tag    =  %x02                 ; tag of 2
printer-attributes-tag =  %x04                ; tag of 4
unsupported-attributes-tag =  %x05            ; tag of 5
