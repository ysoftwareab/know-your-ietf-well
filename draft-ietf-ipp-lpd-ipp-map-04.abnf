print-waiting-jobs = %x01 printer-name LF

receive-job = %x02 printer-name LF

abort-job = %x1 LF

receive-control-file = %x2 number-of-bytes SP name-of-control-file LF
number-of-bytes = 1*DIGIT
name-of-control-file = "cfA" job-number client-host-name
                          ; e.g. "cfA123woden"
job-number = 3DIGIT
client-host-name = <a host name>

receive-data-file = %x03 number-of-bytes SP name-of-data-file LF
number-of-bytes = 1*DIGIT
name-of-data-file = "df" letter job-number client-host-name
              ; e.g. "dfA123woden for the first file
letter = %x41-5A /  %x61-7A    ;  "A" to "Z", "a" to "z"
                                 ;  first file is "A",
                                 ; second "B", and  52nd file is "z"
job-number = 3DIGIT
client-host-name = <a host name>

send-queue-short  = %x03 printer-name *(SP(user-name / job-number)) LF
send-queue-long = %x04 printer-name *(SP(user-name / job-number)) LF

remove-jobs = %x05 printer-name SP agent
                      *(SP(user-name / job-number)) LF

status-response = empty-queue / nonempty-queue
empty-queue = "no-entries" LF
nonempty-queue = printer-status LF heading LF *(job LF)
printer-status =  OK-status / error-status
OK-status = printer-name SP "ready and printing" LF
error-status = < implementation dependent status information >
heading = "Rank" 3SP "Owner" 6SP "Job" 13SP "Files"
                  23SP "Total Size" LF
                     ; the column headings and their values below begin
job = rank *SP owner *SP job *SP files *SP total-size "bytes"
                    ; jobs are in order of oldest to newest
rank = "active" / "1st" / "2nd" / "3rd" / integer "th"
                    ; job that is printing is "active"
                    ; other values show position in the queue
owner = <user name of person who submitted the job>
job = 1*3DIGIT   ; job-number
files = <file name> *( "," <file name>) ; truncated to 24 characters
total-size = 1*DIGIT  ; combined size in bytes of all documents

status-response = empty-queue / nonempty-queue
empty-queue = "no-entries" LF
nonempty-queue = printer-status LF  *job
printer-status =  OK-status / error-status
OK-status = printer-name SP "ready and printing" LF
error-status = < implementation dependent status information >
job = LF line-1 LF line-2 LF
line-1 = owner ":" SP rank 1*SP "[job" job SP host "]"
line-2 =  file-name 1*SP document-size "bytes"
        ; jobs are in order of oldest to newest
rank = "active" / "1st" / "2nd" / "3rd" / integer "th"
          ; job that is printing is "active"
          ; other values show position in the queue
owner = <user name of person who submitted the job>
job = 1*3DIGIT
file-name = [ 1*DIGIT  "copies of" SP ] <file name>
                ; truncated to 24 characters
document-size = 1*DIGIT  ;size of single copy of the document.

